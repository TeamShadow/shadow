options {
  CACHE_TOKENS=true;
  JAVA_UNICODE_ESCAPE=true;
  STATIC=false;
  NODE_USES_PARSER=true;
  MULTI=true;
  VISITOR=true;
  VISITOR_EXCEPTION="ShadowException";
  VISITOR_DATA_TYPE="Boolean";
}

PARSER_BEGIN(ShadowParser)
package shadow.parser.javacc;

import java.io.*;
import shadow.parser.*;
import shadow.AST.ASTWalker;
import shadow.typecheck.type.Type;
import shadow.typecheck.type.Modifiers;
import shadow.Loggers;
import org.apache.logging.log4j.Logger;

/**
 * Grammar to parse Shadow
 * @author William R. Speirs
 * @author Barry Wittman
 * @author Jacob Young
 */
public class ShadowParser
{
   	public static enum TypeKind
   	{		
		CLASS,
		ENUM,
		//ERROR,
		EXCEPTION,
		INTERFACE,
		SINGLETON,
		//VIEW
	};

   private static final Logger logger = Loggers.PARSER;
   	public void debugPrint(String str)
   	{
        logger.trace(str + ": " + token);
    }   	
}

PARSER_END(ShadowParser)

/* WHITE SPACE */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

/* RESERVED WORDS AND LITERALS */

TOKEN :
{
  < ABSTRACT: "abstract" >
| < AND: "and" >
| < ASSERT: "assert" >
| < BOOLEAN: "boolean" >
| < BREAK: "break" >
| < BYTE: "byte" >
| < CASE: "case" >
| < CAST: "cast" >
| < CATCH: "catch" >
| < CHECK: "check" >
| < CLASS: "class" >
| < CODE: "code" >
| < COPY: "copy" >
| < CREATE: "create" >
| < CONSTANT: "constant" >
| < CONTINUE: "continue" >
| < _DEFAULT: "default" >
| < DESTROY: "destroy" >
| < DO: "do" >
| < DOUBLE: "double" >
| < ELSE: "else" >
| < ENUM: "enum" >
//| < ERROR: "error" >
| < EXCEPTION: "exception" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < FINALLY: "finally" >
| < FLOAT: "float" >
| < FOR: "for" >
| < FOREACH: "foreach" >
| < FREEZE: "freeze" >
| < GET: "get" >
| < IF: "if" >
| < IMMUTABLE: "immutable" >
| < IMPLEMENTS: "implements" >
| < IMPORT: "import" >
| < IN: "in" >
//| < INSTANCE: "instance" >
| < IS: "is" >
| < INT: "int" >
| < INTERFACE: "interface" >
| < LOCKED: "locked" >
| < LONG: "long" >
| < NATIVE: "native" >
| < NULL: "null" >
| < NULLABLE: "nullable" >
| < OR: "or" >
| < PRIVATE: "private" >
| < PROTECTED: "protected" >
| < PUBLIC: "public" >
| < READONLY: "readonly" >
| < RECOVER: "recover" >
| < RETURN: "return" >
| < SET: "set" >
| < SHORT: "short" >
| < SINGLETON: "singleton" >
| < _SKIP: "skip" >
| < SUPER: "super" >
| < SWITCH: "switch" >
| < THIS: "this" >
| < THROW: "throw" >
| < THROWS: "throws" >
| < TRUE: "true" >
| < TRY: "try" >
| < UBYTE: "ubyte" >
| < UINT: "uint" >
| < ULONG: "ulong" >
| < USHORT: "ushort" >
| < VAR: "var" >
| < WEAK: "weak" >
| < WHILE: "while" >
| < XOR: "xor" >
}

/* LITERALS */

TOKEN :
{
   < BYTE_LITERAL: //can't end with b or B because of hex digits
	    <DECIMAL_LITERAL> ["y","Y"]
      | <HEX_LITERAL> ["y","Y"]
      | <BINARY_LITERAL> ["y","Y"]
      | <OCTAL_LITERAL> ["y","Y"]    
  >
|
  < UBYTE_LITERAL: //can't end with b or B because of hex digits
        <DECIMAL_LITERAL> ["u","U"]["y","Y"]
      | <HEX_LITERAL> ["u","U"]["y","Y"]
      | <BINARY_LITERAL> ["u","U"]["y","Y"]
      | <OCTAL_LITERAL> ["u","U"]["y","Y"]
  >
|
  < SHORT_LITERAL:
  	("-")?
  	(  	  
        <DECIMAL_LITERAL> ["s","S"]
      | <HEX_LITERAL> ["s","S"]
      | <BINARY_LITERAL> ["s","S"]
      | <OCTAL_LITERAL> ["s","S"]
    )
  >  
|   
  < USHORT_LITERAL:
        <DECIMAL_LITERAL> ["u","U"]["s","S"]
      | <HEX_LITERAL> ["u","U"]["s","S"]
      | <BINARY_LITERAL> ["u","U"]["s","S"]
      | <OCTAL_LITERAL> ["u","U"]["s","S"]
  >  
|
  < INT_LITERAL:
  	  	   
        <DECIMAL_LITERAL> (["i","I"])?
      | <HEX_LITERAL> (["i","I"])?
      | <BINARY_LITERAL> (["i","I"])?
      | <OCTAL_LITERAL> (["i","I"])?
   
  >
|
  < UINT_LITERAL:
        <DECIMAL_LITERAL> ["u","U"](["i","I"])?
      | <HEX_LITERAL> ["u","U"](["i","I"])?
      | <BINARY_LITERAL> ["u","U"](["i","I"])?
      | <OCTAL_LITERAL> ["u","U"](["i","I"])?
  >
|
  < LONG_LITERAL:
  		   
        <DECIMAL_LITERAL> ["l","L"]
      | <HEX_LITERAL> ["l","L"]
      | <BINARY_LITERAL> ["l","L"]
      | <OCTAL_LITERAL> ["l","L"]    
  >
|
  < ULONG_LITERAL:
	  <DECIMAL_LITERAL> ["u","U"]["l","L"]
      | <HEX_LITERAL> ["u","U"]["l","L"]
      | <BINARY_LITERAL> ["u","U"]["l","L"]
      | <OCTAL_LITERAL> ["u","U"]["l","L"]	
  >
|
  < #BINARY_LITERAL: "0" ["b","B"] (["0"-"1"])+ >
|
  < #DECIMAL_LITERAL: "0" | ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" ["c","C"] (["0"-"7"])* > //doesn't use 0o or 0O because it's hard to read
|
  < FLOAT_LITERAL:
       (["0"-"9"])+ "." (["0"-"9"])+ (<EXPONENT>)? ["f","F"]
      | "." (["0"-"9"])+ (<EXPONENT>)? ["f","F"]
      | (["0"-"9"])+ <EXPONENT> ["f","F"]
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F"]
  >
|
  < DOUBLE_LITERAL:
       (["0"-"9"])+ "." (["0"-"9"])+ (<EXPONENT>)? (["d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < CODE_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
|	
  < VERSION_LITERAL:  (["0"-"9"])+ "." (["0"-"9"])+ "." (["0"-"9"])+ >
}

/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
//       "\u0024",  // this is the $, but we want to use that for versions
       "\u0041"-"\u005a",       
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u005f", //underscore (not a legal way to start a variable in Shadow)
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

/* SEPARATORS */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < AT: "@" >
| < ARROW: "->" >
| < YIELDS: "=>" >
}

/* OPERATORS */

TOKEN :
{
  < ASSIGN: "=" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COALESCE: "??" >
| < COLON: ":" >
| < EQ: "==" >
| < REF_EQ: "===" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < REF_NE: "!==" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < BIT_XOR: "^" >
| < REM: "%" >
| < LEFTSHIFT: "<<" >
| < LEFTROTATE: "<<<" >
| < CAT: "#" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < CATASSIGN: "#=" >
| < LEFTSHIFTASSIGN: "<<=" >
| < RIGHTSHIFTASSIGN: ">>=" >
| < RIGHTROTATEASSIGN: ">>>=" >
| < LEFTROTATEASSIGN: "<<<=" >
| < VERSION: "$" >
}

/* >'s need special attention due to syntax for generics */
TOKEN :
{
  < RIGHTROTATE: ">>>" >
  {
     matchedToken.kind = GT;
     ((GTToken)matchedToken).realKind = RIGHTROTATE;
     input_stream.backup(2); 
  }
| < RIGHTSHIFT: ">>" >
  { 
     matchedToken.kind = GT;
     ((GTToken)matchedToken).realKind = RIGHTSHIFT;
     input_stream.backup(1);
  }
| < GT: ">" >
}


/*****************************************
 * THE SHADOW LANGUAGE GRAMMAR STARTS HERE *
 *****************************************/

/*
 * Program structuring syntax follows.
 */

ASTCompilationUnit CompilationUnit():
{
	Modifiers modifiers;
}
{  
  ( ImportDeclaration() )*

  modifiers = Modifiers()
  ( ClassOrInterfaceDeclaration(modifiers)
  | EnumDeclaration(modifiers)  
 // | ViewDeclaration(modifiers)
  )//only one declaration per file
   
  <EOF>
	{
	  	jjtThis.setModifiers(modifiers);
        return jjtThis;
	}
}


void ImportDeclaration():
{
   debugPrint("IMPORT DECLARATION");
}
{
  "import"  Name() ["$" [">" | "<"] < VERSION_LITERAL >] ";" 
}

void GenericDeclaration():
{
   debugPrint("GENERIC DECLARATION");
}
{
  "import" ReferenceType() ";"} 

/*
 * Modifiers. We match all modifiers in a single rule to reduce the chances of
 * syntax errors for simple modifier mistakes. It will also enable us to give
 * better error messages.
 */

Modifiers Modifiers():
{
   Modifiers modifiers = new Modifiers();
   debugPrint("MODIFIERS");
}
{
 (
  LOOKAHEAD(2)
  (
    "public" { modifiers.checkAndAdd(Modifiers.PUBLIC, jjtThis); }
  | "protected" { modifiers.checkAndAdd(Modifiers.PROTECTED, jjtThis); }
  | "private" { modifiers.checkAndAdd(Modifiers.PRIVATE, jjtThis); }
  | "abstract" { modifiers.checkAndAdd(Modifiers.ABSTRACT, jjtThis); }  
  | "readonly" { modifiers.checkAndAdd(Modifiers.READONLY, jjtThis); }
  | "native" { modifiers.checkAndAdd(Modifiers.NATIVE, jjtThis); }
  | "weak" { modifiers.checkAndAdd( Modifiers.WEAK, jjtThis); }
  |	"immutable"{ modifiers.checkAndAdd(Modifiers.IMMUTABLE, jjtThis); }
  |	"nullable" { modifiers.checkAndAdd(Modifiers.NULLABLE, jjtThis); }
  |	"get" { modifiers.checkAndAdd(Modifiers.GET, jjtThis); }
  |	"set" { modifiers.checkAndAdd(Modifiers.SET, jjtThis); }
  | "constant" { modifiers.checkAndAdd(Modifiers.CONSTANT, jjtThis); }
  | "locked" { modifiers.checkAndAdd(Modifiers.LOCKED, jjtThis); }
  )
 )*

 {
 	jjtThis.setModifiers(modifiers);
    return modifiers;
 }
}

/*
 * Declaration syntax follows.
 */
/*
void ViewDeclaration(Modifiers modifiers):
{
	debugPrint("VIEW DECLARATION");
	Token t;

   modifiers.checkViewModifiers(jjtThis);	
}
{
	"view"
	[ LOOKAHEAD(UnqualifiedName() "@") UnqualifiedName() "@" ] //can we specify packages for views?
	t = <IDENTIFIER> { jjtThis.setImage(t.image); jjtThis.setModifiers(modifiers);  }
	"{"        
        ( ViewBodyDeclaration() )+
    "}"
}

void ViewBodyDeclaration():
{
	debugPrint("VIEW BODY DECLARATION");
	Token t;
}
{	
	( LOOKAHEAD(PrimitiveType() "[") StaticArrayType() | PrimitiveType() ) t = <IDENTIFIER> ";"
	{ jjtThis.setImage(t.image);}
}
*/

void ClassOrInterfaceDeclaration(Modifiers modifiers):
{
   debugPrint("CLASS OR INTERFACE DECLARATION");
   Token t;
}
{
  { jjtThis.setModifiers(modifiers); }
  ( "class"
  	{
  	  jjtThis.setKind(TypeKind.CLASS);
  	  modifiers.checkClassModifiers(jjtThis);	  
  	}
	| "singleton"
 	{
 	  jjtThis.setKind(TypeKind.SINGLETON);
 	  modifiers.checkSingletonModifiers(jjtThis);
 	}
  	
 	| "exception"
 	{
 	  jjtThis.setKind(TypeKind.EXCEPTION);
 	  modifiers.checkExceptionModifiers(jjtThis);
 	}
 /*	| "error"
 	{ jjtThis.setKind(TypeKind.ERROR);
 	  modifiers.checkErrorModifiers(jjtThis);
 	}*/
 	| "interface"
 	{
 	  jjtThis.setKind(TypeKind.INTERFACE);
 	   modifiers.checkInterfaceModifiers(jjtThis); 	   
 	 } )

  [ LOOKAHEAD(UnqualifiedName() "@") UnqualifiedName() "@" ] //package specification
  t = <IDENTIFIER> { jjtThis.setImage(t.image); jjtThis.setModifiers(modifiers); }
  [ TypeParameters() ]
  [ Version() ]
  [ ExtendsList(jjtThis.getKind()) ]
  [ ImplementsList(jjtThis.getKind()) ]
  ClassOrInterfaceBody(jjtThis.getKind())
  {
    jjtThis.setLineStart(t.beginLine);
    jjtThis.setColumnStart(t.beginColumn);  }
}

void ExtendsList(TypeKind kind):
{
   boolean extendsMoreThanOne = false;
   debugPrint("EXTENDS LIST");
}
{ 
   "extends" ClassOrInterfaceType()
   ( "," ClassOrInterfaceType() { extendsMoreThanOne = true; } )*
   {
      if (extendsMoreThanOne && kind != TypeKind.INTERFACE)
         throw new ParseException("A class cannot extend more than one other class",jjtThis);
    /*  if (kind == TypeKind.ERROR)
         throw new ParseException("An error cannot extend another type",jjtThis);
      if (kind == TypeKind.SINGLETON)
         throw new ParseException("A singleton cannot extend another type",jjtThis); */
   }
}

void ImplementsList(TypeKind kind):
{ debugPrint("IMPLEMENTS"); }
{
   "implements" ClassOrInterfaceType()
   ( "," ClassOrInterfaceType() )*
   {
      if (kind == TypeKind.INTERFACE)
         throw new ParseException("An interface cannot implement other interfaces",jjtThis);
     /* if( kind == TypeKind.ERROR )
      	throw new ParseException("An error cannot implement interfaces",jjtThis); */
      if( kind == TypeKind.EXCEPTION )
      	throw new ParseException("An exception cannot implement interfaces",jjtThis);
   }
}

void Version():
{ debugPrint("VERSION"); }
{
   "$" < VERSION_LITERAL >
}

void EnumDeclaration(Modifiers modifiers):
{
    debugPrint("ENUM DECLARATION");
    Token t;
	modifiers.checkEnumModifiers(jjtThis);
}
{
{ jjtThis.setModifiers(modifiers); }

  "enum"
  [ LOOKAHEAD(UnqualifiedName() "@") UnqualifiedName() "@" ]
  t = <IDENTIFIER> { jjtThis.setImage(t.image);  }
  [ ImplementsList(TypeKind.ENUM) ]
  EnumBody()
}

void EnumBody():
{ debugPrint("ENUM BODY"); Token t; }
{
   "{"
   EnumConstant() ( "," EnumConstant() )*
   [ ";" ( ClassOrInterfaceBodyDeclaration(TypeKind.ENUM) )* ]
   t = "}"
   {
     jjtThis.setEndToken(t);   }
}

void EnumConstant():
{
	debugPrint("ENUM CONSTANT");
	Token t;
}
{
  t = <IDENTIFIER>  { jjtThis.setImage(t.image); } [ Arguments() ] [ ClassOrInterfaceBody(TypeKind.ENUM) ]
}

void TypeParameters():
{ debugPrint("TYPE PARAMETERS"); Token t; }
{
   "<" TypeParameter() ( "," TypeParameter() )* t = ">" { jjtThis.setEndToken(t); }
}

void TypeParameter():
{
	debugPrint("TYPE PARAMETER");
	Token t;
}
{
   t = <IDENTIFIER>  { jjtThis.setImage(t.image); } [ TypeBound() ]
   {
     jjtThis.addModifier(Modifiers.TYPE_NAME);   }
}

void TypeBound():
{ debugPrint("TYPE BOUND"); }
{
   "is" ClassOrInterfaceType() ( "and" ClassOrInterfaceType() )*
}

void ClassOrInterfaceBody(TypeKind kind):
{ debugPrint("CLASS OR INTERFACE BODY"); Token t; }
{
  "{" ( ClassOrInterfaceBodyDeclaration(kind) )* t = "}"
  {    jjtThis.setEndToken(t);
  }
}

void ClassOrInterfaceBodyDeclaration(TypeKind kind):
{
   Modifiers modifiers;
   debugPrint("CLASS OR INTERFACE BODY DECLARATION"); 
}
{  
// Just get all the modifiers out of the way. If you want to do
// more checks, pass the modifiers down to the member  
	modifiers = Modifiers()
  (  ClassOrInterfaceDeclaration(modifiers)
    | EnumDeclaration(modifiers)
    | LOOKAHEAD( "create" ) CreateDeclaration(modifiers)
    | LOOKAHEAD( "destroy" ) DestroyDeclaration(modifiers)
    | LOOKAHEAD( FieldDeclaration(modifiers) ) FieldDeclaration(modifiers) 
    | MethodDeclaration(modifiers)
    | GenericDeclaration()
    //| ViewDeclaration(modifiers)
  )
}

void FieldDeclaration(Modifiers modifiers):
{
    debugPrint("FIELD DECLARATION");
	modifiers.checkFieldModifiers(jjtThis);
}
{
  { jjtThis.setModifiers(modifiers); }
  Type(modifiers)
  VariableDeclarator() ("," VariableDeclarator() )*
  ";"
}

void VariableDeclarator():
{
  debugPrint("VARIABLE DECLARATOR");
  Token t;
}
{
   t = <IDENTIFIER> [ "=" VariableInitializer() ]
   { jjtThis.setImage(t.image); }
}

void VariableInitializer():
{ debugPrint("VARIABLE INITIALIZER"); }
{ 
  ArrayInitializer()
| ConditionalExpression()
}

void ArrayInitializer():
{ debugPrint("ARRAY INITIALIZER"); Token t; }
{  	
  LOOKAHEAD(2) "{" ArrayInitializer() ( LOOKAHEAD(2) "," ArrayInitializer() )*  [ "," ] t = "}" { jjtThis.setEndToken(t); }
| "{" ConditionalExpression() ( LOOKAHEAD(2) "," ConditionalExpression() )*  [ "," ] t = "}" { jjtThis.setEndToken(t); }
}

void MethodDeclarator(boolean isLocal):
{
	debugPrint("METHOD DECLARATOR");
	Token t;	
}
{
	{ jjtThis.setLocal(isLocal); }  
  t = <IDENTIFIER> { jjtThis.setImage(t.image); } [ TypeParameters() ] FormalParameters(isLocal) "=>" ResultTypes()
}


/*
void InlineMethodDeclarator():
{
	debugPrint("INLINE METHOD DECLARATOR");
	Token t;
}
{
  t = <IDENTIFIER> { jjtThis.setImage(t.image); } [ TypeParameters() ] FormalParameters(true) "=>" InlineResults()
}
*/
	
void InlineResults():
{
	debugPrint("INLINE RESULTS");
	Token t;
}
{
 "(" [ ConditionalExpression() ( "," ConditionalExpression() )* ] t = ")" {
   jjtThis.setEndToken(t); }
}

void FormalParameters(boolean isLocal):
{
  debugPrint("FORMAL PARAMETERS");
  Token t;   
 }
{
	{ jjtThis.setLocal(isLocal); }
  "(" [ FormalParameter() ( "," FormalParameter() )* ] t = ")"
  {
   jjtThis.setEndToken(t);
  }
}

void FormalParameter():
{
    debugPrint("FORMAL PARAMETER");
	Modifiers modifiers;
	Token t;
}
{
   modifiers = Modifiers()
   Type(modifiers) t = <IDENTIFIER> 
   {
     modifiers.checkParameterAndReturnModifiers(jjtThis);
     jjtThis.setModifiers(modifiers);
     jjtThis.setImage(t.image);
     jjtThis.setEndToken(t);     
   }
}

void MethodDeclaration(Modifiers modifiers):
{
  debugPrint("METHOD DECLARATION");
	modifiers.checkMethodModifiers(jjtThis); 
 }
{
  { jjtThis.setModifiers(modifiers); }  
  MethodDeclarator(false) [ "throws" NameList() ]
  ( Block() { jjtThis.setBlock(true); } | ";" )
}

void CreateDeclaration(Modifiers modifiers):
{
  debugPrint("CREATE DECLARATION");
  modifiers.checkCreateModifiers(jjtThis);  
}
{
  { jjtThis.setModifiers(modifiers); jjtThis.setImage("create"); }  
  // Modifiers matched in the caller
  CreateDeclarator() [ "throws" NameList() ]
  ( CreateBlock() { jjtThis.setBlock(true); } | ";" )
}

void CreateDeclarator():
{
	debugPrint("CREATE DECLARATOR");
	Token t;
}
{
  "create" { jjtThis.setImage("create"); } FormalParameters(false)
}

void CreateBlock():
{
  debugPrint("CREATE BLOCK");
  Token t;    
}
{
	"{" [ LOOKAHEAD(ExplicitCreateInvocation()) ExplicitCreateInvocation() ]
    ( BlockStatement() )*  t = "}"
    {
      jjtThis.setEndToken(t);    } }

void DestroyDeclaration(Modifiers modifiers):
{
  debugPrint("DESTROY DECLARATION");
  modifiers.checkDestroyModifiers(jjtThis);
}
{
  { jjtThis.setModifiers(modifiers); jjtThis.setImage("destroy"); }  
  // Modifiers matched in the caller
  "destroy" //no (checked) exceptions from destroys!
  ( Block() { jjtThis.setBlock(true); } | ";" )
}

void ExplicitCreateInvocation():
{
  debugPrint("EXPLICIT CREATE INVOCATION");
Token t;
}
{
  ( "this" { jjtThis.setImage("this"); } | "super" { jjtThis.setImage("super"); } )
 "(" [ ConditionalExpression() ( "," ConditionalExpression() )* ] t = ")"
 ";"
	{
   	jjtThis.setEndToken(t);
 	}
   
}

/*
 * Type, name and expression syntax follows.
 */

void Type(Modifiers modifiers):
{
  	debugPrint("TYPE");
	jjtThis.setModifiers(modifiers);
}
{
   LOOKAHEAD(ReferenceType()) ReferenceType()
 //   |   LOOKAHEAD(PrimitiveType() "[") StaticArrayType()
    |   PrimitiveType()
    |	FunctionType()
}

void ReferenceType():
{ debugPrint("REFERENCE TYPE"); Token t; }
{
    PrimitiveType() ( LOOKAHEAD(2) "[" ( "," { jjtThis.incrementDimensions(); } )* { jjtThis.pushDimensions(); } t = "]" { jjtThis.setEndToken(t); }  )+
  | ClassOrInterfaceType() ( LOOKAHEAD(2) "[" ( "," { jjtThis.incrementDimensions(); } )* { jjtThis.pushDimensions(); } t = "]" { jjtThis.setEndToken(t); } )*
}

void FunctionType():
{
	debugPrint("FUNCTION TYPE");
}
{
    ResultTypes() "=>" ResultTypes()
    {
      jjtThis.addModifier(Modifiers.TYPE_NAME);    }
}

void ClassOrInterfaceType():
{
	debugPrint("CLASS OR INTERFACE TYPE");
	StringBuffer sb = new StringBuffer();
	Token t;
}
{
   [ LOOKAHEAD(UnqualifiedName() "@") UnqualifiedName() "@"  ] ClassOrInterfaceTypeSuffix() (  LOOKAHEAD(2) ":" ClassOrInterfaceTypeSuffix() )*
}

void ClassOrInterfaceTypeSuffix():
{
	debugPrint("CLASS OR INTERFACE TYPE SUFFIX");	
	Token t;  
}
{
   t = <IDENTIFIER> [ LOOKAHEAD(TypeArguments()) TypeArguments() ]
   { jjtThis.setImage(t.image); }  
}

void TypeArguments():
{ debugPrint("TYPE ARGUMENTS"); Token t;}
{
   "<" Type(Modifiers.NO_MODIFIERS) ( "," Type(Modifiers.NO_MODIFIERS) )* t = ">" { jjtThis.setEndToken(t); }
}


void PrimitiveType():
{
  	debugPrint("PRIMITIVE TYPE");
	Token t;
}
{

(    
  t = "boolean"
| t = "byte"
| t = "code"
| t = "double"
| t = "float"
| t = "int"
| t = "long"
| t = "short"
| t = "ubyte"
| t = "uint" 
| t = "ulong" 
| t = "ushort" 
)
{
  jjtThis.setImage(t.image);
  jjtThis.addModifier(Modifiers.TYPE_NAME);}
}

void ResultType():
{ debugPrint("RESULT TYPE");
	Modifiers modifiers;
}
{
	modifiers = Modifiers() Type(modifiers) [ <IDENTIFIER > ] //optional identifier used only for documentation
	{
	  modifiers.checkParameterAndReturnModifiers(jjtThis);
	  jjtThis.setModifiers(modifiers);	} 
}

void ResultTypes():
{
  	debugPrint("RESULT TYPES");
	Token t;
}
{
  "(" [ ResultType() ( "," ResultType() )* ] t = ")"
  {
   jjtThis.setEndToken(t);
 }
}

void Name():
{
	debugPrint("NAME");
	StringBuffer sb = new StringBuffer();
	Token t;
}
{
  [ LOOKAHEAD(UnqualifiedName() "@") UnqualifiedName() "@" ] t = <IDENTIFIER> { sb.append(t.image); debugPrint(t.image); } ( LOOKAHEAD(2) ":" t = <IDENTIFIER> { sb.append(':').append(t.image); } )*
  { jjtThis.setImage(sb.toString()); }
}

void UnqualifiedName():
{
	debugPrint("UNQUALIFIED NAME");
	StringBuffer sb = new StringBuffer();
	Token t;
}
{
  "default"  { jjtThis.setImage("default");  }
| t = <IDENTIFIER> { sb.append(t.image); debugPrint(t.image); } ( LOOKAHEAD(2) ":" t = <IDENTIFIER> { sb.append(':').append(t.image); } )*
  { jjtThis.setImage(sb.toString()); }
}

void NameList():
{ debugPrint("NAME LIST"); }
{
  ClassOrInterfaceType() ( "," ClassOrInterfaceType() )*
}


/*
 * Expression syntax follows.
 */

void AssignmentOperator():
{ debugPrint("ASSIGNMENT OPERATOR"); }
{
    "="    { jjtThis.setImage("="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.EQUAL); }
  | "#="   { jjtThis.setImage("#="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.CAT); }
  | "*="   { jjtThis.setImage("*="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.STAR); }
  | "/="   { jjtThis.setImage("/="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.SLASH); }
  | "%="   { jjtThis.setImage("%="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.MOD); }
  | "+="   { jjtThis.setImage("+="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.PLUS); }
  | "-="   { jjtThis.setImage("-="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.MINUS); }
  | "<<="  { jjtThis.setImage("l="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.LEFT_SHIFT); }
  | ">>="  { jjtThis.setImage("r="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.RIGHT_SHIFT); }
  | ">>>=" { jjtThis.setImage("L="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.RIGHT_ROTATE); }
  | "<<<=" { jjtThis.setImage("R="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.LEFT_ROTATE); }
  | "&="   { jjtThis.setImage("&="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.AND); }
  | "^="   { jjtThis.setImage("^="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.XOR); }
  | "|="   { jjtThis.setImage("|="); jjtThis.setAssignmentType(ASTAssignmentOperator.AssignmentType.OR); }
}

void ConditionalExpression():
{}
{
  CoalesceExpression() [ "?" ConditionalExpression() "," ConditionalExpression() ]
}

void CoalesceExpression():
{}
{
  ConditionalOrExpression()  ( "??" ConditionalOrExpression() )*
}

void ConditionalOrExpression():
{}
{
  ConditionalExclusiveOrExpression() ( "or" { jjtThis.addToImage('o'); } ConditionalExclusiveOrExpression() )*
}


void ConditionalExclusiveOrExpression():
{}
{
  ConditionalAndExpression() ( "xor" { jjtThis.addToImage('x'); } ConditionalAndExpression() )*
}

void ConditionalAndExpression():
{}
{
  BitwiseOrExpression() ( "and" { jjtThis.addToImage('a'); } BitwiseOrExpression() )*
}

void BitwiseOrExpression():
{}
{
  BitwiseExclusiveOrExpression() ( "|" { jjtThis.addToImage('|'); } BitwiseExclusiveOrExpression() )*
}

void BitwiseExclusiveOrExpression():
{}
{
  BitwiseAndExpression() ( "^" { jjtThis.addToImage('^'); } BitwiseAndExpression() )*
}

void BitwiseAndExpression():
{}
{
  EqualityExpression() ( "&" { jjtThis.addToImage('&'); } EqualityExpression() )*
}

void EqualityExpression():
{}
{
  IsExpression() ( ( "==" { jjtThis.addToImage('='); } |
  					 "===" { jjtThis.addToImage('e'); } |
                     "!=" { jjtThis.addToImage('!'); } |
                     "!==" { jjtThis.addToImage('n'); } ) IsExpression() )*
}

void IsExpression():
{}
{
  RelationalExpression() [ "is" { jjtThis.addToImage('i'); } Type(Modifiers.NO_MODIFIERS) ]
}

void RelationalExpression():
{ debugPrint("RELATIONAL EXPRESSION"); }
{
  ConcatenationExpression() ( ( "<" { jjtThis.addToImage('<'); } |
                        ">" { jjtThis.addToImage('>'); } |
                        "<=" { jjtThis.addToImage('{'); } |
                        ">="  { jjtThis.addToImage('}'); }) ConcatenationExpression() )*
}


void ConcatenationExpression():
{ debugPrint("CONCATENATION EXPRESSION"); }
{
  ShiftExpression() ( "#" { jjtThis.addToImage('#'); }  ShiftExpression() )*
}


// Since shift & rotate are not single characters (see TAC Symbole2Operator code)
// we are using r & l for right & left shift and
// R & L for right & left rotate
void ShiftExpression():
{ debugPrint("SHIFT EXPRESSION"); }
{
  RotateExpression()
  ( ( "<<" { jjtThis.addToImage('l'); } |
      LOOKAHEAD({ getToken(1).kind == GT && ((GTToken)getToken(1)).realKind == RIGHTSHIFT} ) ">" ">" { jjtThis.addToImage('r'); } ) // >> needs special treatment
      RotateExpression() )*
}

void RotateExpression():
{ debugPrint("ROTATE EXPRESSION"); }
{
  AdditiveExpression()
  ( ( "<<<" { jjtThis.addToImage('L'); } |
      LOOKAHEAD({ getToken(1).kind == GT && ((GTToken)getToken(1)).realKind == RIGHTROTATE} ) ">" ">" ">"  { jjtThis.addToImage('R'); } ) // >>> needs special treatment
      AdditiveExpression() )*
}


void AdditiveExpression():
{}
{
  MultiplicativeExpression()
  ( ( "+" { jjtThis.addToImage('+'); } |
      "-" { jjtThis.addToImage('-'); } ) MultiplicativeExpression() )*
}

void MultiplicativeExpression():
{}
{
  UnaryExpression()
  ( ( "*" { jjtThis.addToImage('*'); } |
      "/" { jjtThis.addToImage('/'); } |
      "%" { jjtThis.addToImage('%'); } ) UnaryExpression() )*
}

void UnaryExpression():
{}
{
  //removed unary + since it's useless   
 "#" { jjtThis.setImage("#"); } UnaryExpression()
| "~" { jjtThis.setImage("~"); } UnaryExpression()
| "!" { jjtThis.setImage("!"); } UnaryExpression()
//| LOOKAHEAD(PrimaryExpression()) PrimaryExpression() //covers cases in which literals start with -
| "-" { jjtThis.setImage("-"); } UnaryExpression()
| LOOKAHEAD(FormalParameters(true) "=>") InlineMethodDefinition()
| PrimaryExpression()
}

void InlineMethodDefinition():
{ debugPrint("INLINE METHOD DEFINITION"); }
{
  FormalParameters(true) "=>" InlineResults() //inline method definition 
}

void CastExpression():
{
    debugPrint("CAST EXPRESSION");
    jjtThis.setImage("cast< >");
    Token t;   
}
{
 "cast" "<" Type(Modifiers.NO_MODIFIERS) ">" "(" ConditionalExpression() t = ")"
 {
   jjtThis.setEndToken(t);
 }
}

void CheckExpression():
{
    debugPrint("CHECK EXPRESSION");
    jjtThis.setImage("check");
    Token t;   
}
{ 
 "check" "(" ConditionalExpression() t = ")"
 {
   jjtThis.setEndToken(t);
 }
}

void CopyExpression():
{
    debugPrint("COPY EXPRESSION");
    Token t;        
}
{

	(  "copy" { jjtThis.setImage("copy"); } 
|    "freeze" { jjtThis.setImage("freeze"); }
	)
	
  "(" ConditionalExpression() t = ")"
  {
   jjtThis.setEndToken(t);
 }
}

void PrimaryExpression():
{ debugPrint("PRIMARY EXPRESSION"); }
{  
	PrimaryPrefix() ( LOOKAHEAD(PrimarySuffix()) PrimarySuffix() )* 
}

void RightSide():
{
  debugPrint("RIGHT SIDE");
}
{
	LOOKAHEAD( SequenceRightSide() ) SequenceRightSide()
 | 	ConditionalExpression()
}

void SequenceRightSide():
{
  debugPrint("SEQUENCE RIGHT SIDE");
  Token t;
}
{
 	"("	ConditionalExpression() (","  ConditionalExpression() )+ t = ")"
 	{
	   jjtThis.setEndToken(t);
	 }
}

void SequenceLeftSide():
{
  debugPrint("SEQUENCE LEFT SIDE");
  Token t;
}
{
  	"(" { { jjtThis.addItem();}	} [ (LOOKAHEAD(SequenceVariable()) SequenceVariable() | PrimaryExpression()) { jjtThis.setUsed();} ] ("," { { jjtThis.addItem();}	} [(LOOKAHEAD(SequenceVariable()) SequenceVariable() | PrimaryExpression()) { jjtThis.setUsed();}] )* t = ")"
  	{
   jjtThis.setEndToken(t);
 }
	
}

void SequenceVariable() :
{
  debugPrint("SEQUENCE VARIABLE");
  Modifiers modifiers;
  Token t;}
{
  modifiers = Modifiers() ( "var" | Type(modifiers) )  t = <IDENTIFIER>
  {
	jjtThis.setImage(t.image);
	modifiers.checkLocalVariableModifiers(jjtThis);
	jjtThis.setModifiers(modifiers);
  }}



void PrimaryPrefix():
{
	debugPrint("PRIMARY PREFIX");
	Token t;
}
{
  Literal()
| "this" { jjtThis.setImage("this"); }
| "super" { jjtThis.setImage("super"); }
| CheckExpression()
| CopyExpression()
| CastExpression()
| LOOKAHEAD( "(" ConditionalExpression() ")" ) "(" ConditionalExpression() t = ")" { jjtThis.setEndToken(t); }
| PrimitiveType()
| FunctionType()
| [LOOKAHEAD(UnqualifiedName() "@") UnqualifiedName() "@" ] t = <IDENTIFIER> { jjtThis.setImage(t.image); debugPrint(t.image); }
//last production catches class types and identifiers
}

void PrimarySuffix():
{
	debugPrint("PRIMARY SUFFIX");
	Token t;
}
{
  LOOKAHEAD(2) QualifiedKeyword()
| LOOKAHEAD(3) Brackets()
| LOOKAHEAD(3) Subscript()
| LOOKAHEAD(2) Destroy()
| Method()
| LOOKAHEAD(Allocation()) Allocation()
| LOOKAHEAD(ScopeSpecifier()) ScopeSpecifier()
| Property()
| LOOKAHEAD( [ TypeArguments() ] "(" ) MethodCall()
}

void Allocation():
{
  debugPrint("ALLOCATION");
  Token t;}
{
  		//LOOKAHEAD(2) Instance()
	/*|*/	LOOKAHEAD(ArrayCreate()) ArrayCreate()
	|   Create()
 }


void QualifiedKeyword():
{
  	debugPrint("QUALIFIED KEYWORD");
	Token t;
}
{    
  ":"
  ( "this" { jjtThis.setImage("this"); }
	| "super" { jjtThis.setImage("super"); }	
	)
// When you have inner classes, you sometimes need to know Outer:this or Inner:this
// same for Outer:super or Inner:super
// Prefix must be a type name that you're currently inside of
}

void Brackets():
{
  debugPrint("BRACKETS");
  Token t;
}
{
  ( LOOKAHEAD(2) "[" ("," { jjtThis.incrementDimensions(); })* t = "]" { jjtThis.pushDimensions(); jjtThis.setEndToken(t); } )+
}

void ArrayCreate():
{
  debugPrint("ARRAY CREATE");
  Token t;
}
{
  [ TypeArguments() ] ":"
  ("create" { jjtThis.setImage("create"); } | "null" { jjtThis.setImage("null"); } )
  ArrayDimensions()
  [LOOKAHEAD(2) ( LOOKAHEAD(2) ":" "(" [ ConditionalExpression() ( "," ConditionalExpression() )* ] t = ")" { jjtThis.setCreate(); jjtThis.setEndToken(t); } |
     ":" "default" "(" ConditionalExpression() t = ")" { jjtThis.setDefault(); jjtThis.setEndToken(t); })]
}

void ArrayDimensions():
{
  debugPrint("ARRAY DIMENSIONS");
  Token t;
}
{ 
  ( LOOKAHEAD(2) "[" ConditionalExpression() ("," ConditionalExpression() { jjtThis.incrementDimensions(); })* t = "]" { jjtThis.pushDimensions(); jjtThis.setEndToken(t);})+
}

void Subscript():
{
  debugPrint("SUBSCRIPT");
  Token t;
}
{  
  "[" ConditionalExpression() ("," ConditionalExpression() { jjtThis.incrementDimensions(); })* t = "]" { jjtThis.pushDimensions(); jjtThis.setEndToken(t); }
}

void Create():
{
  	debugPrint("CREATE");
  	Token t;
}
{
  [ TypeArguments() ] ":" "create" "(" [ ConditionalExpression() ( "," ConditionalExpression() )* ] t = ")"
  { jjtThis.setImage("create"); debugPrint("create"); jjtThis.setEndToken(t);  } 
}

/*
void Instance():
{
  debugPrint("INSTANCE");
}
{
 	":" "instance" { jjtThis.setImage("instance"); debugPrint("instance");} 	
}
*/

void ScopeSpecifier():
{
  	debugPrint("SCOPE SPECIFIER");
	Token t;
}
{
	[ TypeArguments() ] ":" (t = <IDENTIFIER> | t = "class") { jjtThis.setImage(t.image); debugPrint(t.image);} //field, constant, or class
}

void Destroy():
{
  	debugPrint("DESTROY");
	Token t;
}
{
  "." "destroy" { jjtThis.setImage("destroy"); debugPrint("destroy");  }
}

void Method():
{
  	debugPrint("METHOD");
	Token t;
}
{
  "." t = <IDENTIFIER> { jjtThis.setImage(t.image); debugPrint(t.image);  }
}

void Property():
{
  	debugPrint("PROPERTY");
	Token t;
}
{
  "->" t = <IDENTIFIER> { jjtThis.setImage(t.image); debugPrint(t.image);  }
}

void MethodCall():
{
	debugPrint("METHOD CALL");
	Token t;
}
{
  [ TypeArguments() ] "(" [ ConditionalExpression() ( "," ConditionalExpression() )* ] t = ")"
  {
    jjtThis.setEndToken(t);  }
}




void Literal() :
{
	debugPrint("LITERAL");
	Token t;
	jjtThis.addModifier(Modifiers.IMMUTABLE);
	jjtThis.addModifier(Modifiers.CONSTANT); 
}
{
  (    
  t = <BYTE_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.BYTE); }
| t = <CODE_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.CODE); }
| t = <SHORT_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.SHORT); }
| t = <INT_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.INT); }
| t = <LONG_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.LONG); }
| t = <FLOAT_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.FLOAT); }
| t = <DOUBLE_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.DOUBLE); }
| t = <STRING_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.STRING); }
| t = <UBYTE_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.UBYTE); }
| t = <USHORT_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.USHORT); }
| t = <UINT_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.UINT); }
| t = <ULONG_LITERAL> { jjtThis.setImage(t.image); jjtThis.setLiteral(Literal.ULONG); }
| BooleanLiteral() { jjtThis.setLiteral(Literal.BOOLEAN); }
| NullLiteral() { jjtThis.setLiteral(Literal.NULL); jjtThis.addModifier(Modifiers.NULLABLE); jjtThis.removeModifier(Modifiers.IMMUTABLE); }  )
}

void BooleanLiteral():
{ debugPrint("BOOLEAN LITERAL"); }
{    
  "true" { jjtThis.setTrue(); jjtThis.setImage("true"); }
| "false" { jjtThis.setImage("false"); }
 
}

void NullLiteral():
{ debugPrint("NULL LITERAL"); }
{
  "null" { jjtThis.setImage("null"); }
}

void Arguments():
{ debugPrint("ARGUMENTS");
Token t;
}
{
  "(" [ ConditionalExpression() ( "," ConditionalExpression() )* ] t = ")"
  {	jjtThis.setEndToken(t);
  }
}

/*
 * Statement syntax follows.
 */

void Statement():
{ debugPrint("STATEMENT"); }
{  
  AssertStatement()
| Block()
| EmptyStatement()
| StatementExpression() ";"
| SwitchStatement()
| IfStatement()
| WhileStatement()
| DoStatement()
| ForStatement()
| ForeachStatement()
| BreakOrContinueStatement()
| ReturnStatement()
| ThrowStatement()
| FinallyStatement() //includes try/catch/recover/finally
}

void AssertStatement():
{ debugPrint("ASSERT STATEMENT"); }
{
  "assert" ConditionalExpression() [ "," ConditionalExpression() ] ";"
}

void Block():
{ debugPrint("BLOCK"); Token t; }
{
  "{" ( BlockStatement() )* t = "}"
  {
    jjtThis.setEndToken(t);  }
}

void BlockStatement():
{
    debugPrint("BLOCK STATEMENT");    
}
{
  LOOKAHEAD(LocalDeclaration()) LocalDeclaration()    
| Statement()
// No local classes in Shadow
}

void LocalDeclaration():
{
  debugPrint("LOCAL DECLARATION");
  Modifiers modifiers;}
{
  modifiers = Modifiers()    
  (
    //LOOKAHEAD(InlineMethodDeclaration(modifiers)) InlineMethodDeclaration(modifiers)
    LOOKAHEAD(LocalMethodDeclaration(modifiers)) LocalMethodDeclaration(modifiers)
  | LocalVariableDeclaration(modifiers) ";" )  }

/*
void InlineMethodDeclaration(Modifiers modifiers):
{
  	debugPrint("INLINE METHOD DECLARATION");
	modifiers.checkLocalMethodModifiers(jjtThis); 
 }
{
  { jjtThis.setModifiers(modifiers); }  
  InlineMethodDeclarator() [ "throws" NameList() ]
  ";"
}
*/

void LocalMethodDeclaration(Modifiers modifiers):
{
  	debugPrint("LOCAL METHOD DECLARATION");
	modifiers.checkLocalMethodModifiers(jjtThis); 
 }
{
  { jjtThis.setModifiers(modifiers); }  
  MethodDeclarator(true) [ "throws" NameList() ]
  Block()
}

void LocalVariableDeclaration(Modifiers modifiers):
{
  debugPrint("LOCAL VARIABLE DECLARATION");
  modifiers.checkLocalVariableModifiers(jjtThis);
}
{  
  (  "var" { jjtThis.setImage("var"); }  | Type(modifiers) )
  VariableDeclarator() ("," VariableDeclarator() )*
  {
     jjtThis.setModifiers(modifiers);
  }
}

void EmptyStatement():
{}
{
  "skip" ";"
}

void StatementExpression():
{ debugPrint("STATEMENT EXPRESSION"); }
{
  		LOOKAHEAD( SequenceAssignment() ) SequenceAssignment() 	
	| 	Expression()  //method call, assignment, or allocation
}

void SequenceAssignment() :
{ debugPrint("SEQUENCE ASSIGNMENT"); }
{
  	SequenceLeftSide() "=" RightSide() { jjtThis.setImage("="); }  
}

void Expression() :
{ debugPrint("EXPRESSION"); }
{
	PrimaryExpression() [ AssignmentOperator() { jjtThis.setImage("?="); } ConditionalExpression() ]
}

void SwitchStatement():
{ debugPrint("SWITCH STATEMENT"); Token t; }
{
  "switch" "(" ConditionalExpression() ")" "{"
    ( SwitchLabel() ( Statement() ) )+
  t = "}"
  {    jjtThis.setEndToken(t);
  }
}

void SwitchLabel():
{
	Token t;
}
{
  "case" "(" PrimaryExpression() ("," PrimaryExpression())* t = ")"  {
    jjtThis.setEndToken(t);  }
| "default"
}

void IfStatement():
{ debugPrint("IF STATEMENT"); }
{
  "if" "(" ConditionalExpression() ")" Statement() [ LOOKAHEAD(1) "else" Statement() ]
}

void WhileStatement():
{ debugPrint("WHILE STATEMENT"); }
{
  "while" "(" ConditionalExpression() ")" Statement()
}

void DoStatement():
{ debugPrint("DO STATEMENT");
	Token t;
}
{
  "do" Statement() "while" "(" ConditionalExpression() t = ")" ";"
  {     jjtThis.setEndToken(t);
  }
}

void ForeachStatement():
{
	debugPrint("FOREACH STATEMENT");
	Token t;	
}
{
  "foreach" "(" ForeachInit() ")" Statement()
}

void ForeachInit():
{
	debugPrint("FOREACH INIT");
	Token t;
	Modifiers modifiers;	
}
{
 	modifiers = Modifiers() ( "var" | Type(modifiers) ) t = <IDENTIFIER> { jjtThis.setImage(t.image); } "in" ConditionalExpression()
 	{
 	  modifiers.checkLocalVariableModifiers(jjtThis);
 	  jjtThis.setModifiers(modifiers); 	}
}

void ForStatement() :
{ debugPrint("FOR STATEMENT"); }
{
  "for" "(" [ ForInit() ] ";" ConditionalExpression() ";" [ ForUpdate() ] ")" Statement()
}

void ForInit():
{
	debugPrint("FOR INIT");
	Token t;
	Modifiers modifiers;	
}
{
  LOOKAHEAD( Modifiers() LocalVariableDeclaration(Modifiers.NO_MODIFIERS) ) modifiers = Modifiers() LocalVariableDeclaration(modifiers)
| StatementExpressionList()
}

void StatementExpressionList():
{ debugPrint("STATEMENT EXPRESSION LIST"); }
{
  StatementExpression() ( "," StatementExpression() )*
}

void ForUpdate():
{ debugPrint("FOR UPDATE"); }
{
  StatementExpressionList()
}

void BreakOrContinueStatement():
{	
	debugPrint("BREAK OR CONTINUE STATEMENT");	
}
{
  (    	"break" { jjtThis.setImage("break"); }    
    |	"continue" { jjtThis.setImage("continue"); }
  ) ";"
}

void ReturnStatement():
{ debugPrint("RETURN STATEMENT"); }
{
  "return"
  (
    ";"
  | RightSide() ";"   
  )
}

void ThrowStatement():
{ debugPrint("THROW STATEMENT"); }
{
  "throw" ConditionalExpression() ";"
}



// for a simpler back end, a finally statement holds
// the try, the catches, and the recover (even if no finally is used)
void FinallyStatement():
/*
 * Semantic check required here to make sure that at least one
 * finally/recover/catch is present.
 */
{ debugPrint("FINALLY STATEMENT"); }
{
  RecoverStatement()
  [ "finally" Block() ]
}

void RecoverStatement():
{ debugPrint("RECOVER STATEMENT"); }
{
  CatchStatements()
  [ "recover" Block() ]
}

void CatchStatements():
{ debugPrint("CATCH STATEMENTS"); }
{
  TryStatement()
  ( CatchStatement() )*
}

void CatchStatement():
{ debugPrint("CATCH STATEMENT"); }
{ 
  "catch" "(" FormalParameter() ")" Block()
}

void TryStatement():
{ debugPrint("TRY STATEMENT"); }
{
  "try" Block()
}