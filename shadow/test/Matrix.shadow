class shadow:test@
	Matrix
is  CanAdd<Matrix>
and	CanSubtract<Matrix>
and	CanMultiply<Matrix>
and	CanNegate<Matrix>
and	CanEqual<Matrix>
and	CanIndex<int, double[]>
{
	double[][] values;
	get int rows;
	get int columns;
		
	public create(int rows, int columns )
	{
		this:rows = rows;
		this:columns = columns;
		values = double:create[rows][columns];
	}
	
	public readonly index(int row) => (double[])
	{
		return values[row];	
	}
	
	public readonly add(Matrix other) => (Matrix)
	{
		if( other:rows != rows or other:columns != columns )
			throw IllegalArgumentException:create("Mismatched matrix dimensions");
			
		Matrix result = Matrix:create(rows, columns);	

		for( int i = 0; i < rows; i += 1 )					
			for( int j = 0; j < columns; j += 1 )		
				result[i][j] = this[i][j] + other[i][j];
				
		return result;
	}
	
	public readonly subtract(Matrix other) => (Matrix)
	{
		if( other:rows != rows or other:columns != columns )
			throw IllegalArgumentException:create("Mismatched matrix dimensions");
			
		Matrix result = Matrix:create(rows, columns);
		
		for( int i = 0; i < rows; i += 1 )
			for( int j = 0; j < columns; j += 1 )
				result[i][j] = this[i][j] - other[i][j];
		
		return result;
	}
	
	public readonly multiply(Matrix other) => (Matrix)
	{
		if( other:rows != columns )
			throw IllegalArgumentException:create();
			
		Matrix result = Matrix:create(rows, other:columns);
		
		for( int i = 0; i < rows; i += 1 )
			for( int j = 0; j < other:columns; j += 1 )
				for( int k = 0; k < columns; k += 1 )
					result[i][j] += this[i][k] * other[k][j];
		
		return result;
	
	}
	
	public readonly negate() => (Matrix)
	{		
		Matrix result = Matrix:create(rows, columns);
		
		for( int i = 0; i < rows; i += 1 )
			for( int j = 0; j < columns; j += 1 )				
					result[i][j] = -(this[i][j]);
		
		return result;	
	}	

	public readonly equal(Matrix other) => (boolean)
	{
		if( rows != other:rows or columns != other:columns )
			return false;
			
		for( int i = 0; i < rows; i += 1 )
			for( int j = 0; j < columns; j += 1 )
				if( this[i][j] != other[i][j] )
					return false;
					
		return true;
	}
	
	public readonly toString() => (String)
	{
		var string = MutableString:create();		
		foreach( double[] row in values )
		{					
			string.append( #row );			
			string.append( "\n" );		
		}		
		return #string;
	}
}
