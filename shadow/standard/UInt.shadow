/**
 * Class {@code uint} contains the methods that can be called on {@code uint}  
 * values.
 */
locked immutable class shadow:standard@
	UInt
is	Integer<uint>
and	Integer<ulong>
and	Integer<long>
and	Number<float>
and	Number<double>
and	CanHash
{
	/// Largest value a {@code uint} can have, 4294967295.
	public constant uint MAX =  4294967295u;	
	/// Smallest value a {@code uint} can have, 0.
	public constant uint MIN =  0u;
	
	
	// Methods for Integer<uint>
	
	/**
	 * Returns the bitwise complement of the {@code uint} value.
	 * @return bitwise complement 
	 */ 
	[ImportNative]
	public bitComplement() => (uint);
	
	/** 
	 * Finds the result of left-shifting the bits of the {@code uint} value
	 * by an unsigned amount.
	 * @param amount bits to shift
	 * @return shifted value 
	 */
	[ImportNative]
	public bitShiftLeft(uint amount) => (uint);
	
	/** 
	 * Finds the result of right-shifting the bits of the {@code uint} value
	 * by an unsigned amount.
	 * @param amount bits to shift
	 * @return shifted value 
	 */
	[ImportNative]
	public bitShiftRight(uint amount) => (uint);
	
	/** 
	 * Finds the result of left-rotating the bits of the {@code uint} value
	 * by an unsigned amount.
	 * @param amount bits to rotate
	 * @return rotated value 
	 */
	[ImportNative]
	public bitRotateLeft(uint amount) => (uint);
	
	/** 
	 * Finds the result of right-rotating the bits of the {@code uint} value
	 * by an unsigned amount.
	 * @param amount bits to rotate
	 * @return rotated value 
	 */
	[ImportNative]
	public bitRotateRight(uint amount) => (uint);
	
	/** 
	 * Finds the result of left-shifting the bits of the {@code uint} value
	 * by a signed amount which may be positive, negative, or zero.
	 * @param amount bits to shift
	 * @return shifted value 
	 */
	public bitShiftLeft(int amount) => (uint)
	{
		uint abs = amount.abs();
		return amount >= 0 ? bitShiftLeft(abs), bitShiftRight(abs);
	}
	
	/** 
	 * Finds the result of right-shifting the bits of the {@code uint} value
	 * by a signed amount which may be positive, negative, or zero.
	 * @param amount bits to shift
	 * @return shifted value 
	 */
	public bitShiftRight(int amount) => (uint)
	{
		uint abs = amount.abs();
		return amount >= 0 ? bitShiftRight(abs), bitShiftLeft(abs);
	}
	
	/** 
	 * Finds the result of left-rotating the bits of the {@code uint} value
	 * by a signed amount which may be positive, negative, or zero.
	 * @param amount bits to rotate
	 * @return rotated value 
	 */
	public bitRotateLeft(int amount) => (uint)
	{
		uint abs = amount.abs();
		return amount >= 0 ? bitRotateLeft(abs), bitRotateRight(abs);
	}
	
	/** 
	 * Finds the result of right-rotating the bits of the {@code uint} value
	 * by a signed amount which may be positive, negative, or zero.
	 * @param amount bits to rotate
	 * @return rotated value 
	 */
	public bitRotateRight(int amount) => (uint)
	{
		uint abs = amount.abs();
		return amount >= 0 ? bitRotateRight(abs), bitRotateLeft(abs);
	}
	
	
	// Methods for Number
	
	/** 
	 * Converts the {@code uint} value to a {@code byte}, which will overflow
	 * if the value is too large to be contained in a {@code byte}.	 
	 * @return {@code byte} value 
	 */
	[ImportNative]
	public toByte() => (byte);
	
	/** 
	 * Converts the {@code uint} value to a {@code ubyte}, which will overflow
	 * if the value is too large to be contained in a {@code ubyte}.	 
	 * @return {@code ubyte} value 
	 */
	[ImportNative]
	public toUByte() => (ubyte);
	
	/** 
	 * Converts the {@code uint} value to a {@code short}, which will overflow
	 * if the value is too large to be contained in a {@code short}.
	 * @return {@code short} value 
	 */
	[ImportNative]
	public toShort() => (short);
	
	/** 
	 * Converts the {@code uint} value to a {@code ushort}, which will overflow
	 * if the value is too large to be contained in a {@code ushort}.
	 * @return {@code ushort} value 
	 */
	[ImportNative]
	public toUShort() => (ushort);
	
	/** 
	 * Converts the {@code uint} value to an {@code int}, which will overflow
	 * if the value is too large to be contained in an {@code int}.
	 * @return {@code int} value 
	 */
	[ImportNative]
	public toInt() => (int);
	
	/** 
	 * Converts the {@code uint} value to a {@code uint}, which involves
	 * not change.
	 * @return {@code uint} value 
	 */
	[ImportNative]
	public toUInt() => (uint);
	
	/** 
	 * Converts the {@code uint} value to a {@code long}.
	 * @return {@code long} value 
	 */
	[ImportNative]
	public toLong() => (long);
	
	/** 
	 * Converts the {@code uint} value to a {@code ulong}.
	 * @return {@code ulong} value 
	 */
	[ImportNative]
	public toULong() => (ulong);
	
	/** 
	 * Converts the {@code uint} value to a {@code code}.
	 * @return {@code code} value 
	 */
	[ImportNative]
	public toCode() => (code);
	
	/** 
	 * Converts the {@code uint} value to a {@code float}.
	 * @return {@code float} value 
	 */
	[ImportNative]
	public toFloat() => (float);
	
	/** 
	 * Converts the {@code uint} value to a {@code double}.
	 * @return {@code double} value 
	 */
	[ImportNative]
	public toDouble() => (double);
	
	// Methods for Integer<uint>
	
	/** 
	 * Computes the bitwise OR of the value and another {@code uint} value.
	 * @param other value to bitwise OR with
	 * @return result of bitwise OR 
	 */
	[ImportNative]
	public bitOr(uint other) => (uint);
	
	/** 
	 * Computes the bitwise XOR of the value and another {@code uint} value.
	 * @param other value to bitwise XOR with
	 * @return result of bitwise XOR 
	 */
	[ImportNative]
	public bitXor(uint other) => (uint);
	
	/** 
	 * Computes the bitwise AND of the value and another {@code uint} value.
	 * @param other value to bitwise AND with
	 * @return result of bitwise AND
	 */
	[ImportNative]
	public bitAnd(uint other) => (uint);
	
	// Method for CanAdd<uint>
	/**
	 * Adds another {@code uint} to the current value and returns the resulting value.	 
	 * @param other value to add
	 * @return sum of the two values
	 */
	[ImportNative]
	public add(uint other) => (uint);
	
	// Method for CanSubtract<uint>
	/**
	 * Subtracts another {@code uint} from the current value and returns the resulting value.	 
	 * @param other value to subtract
	 * @return difference of the two values
	 */
	[ImportNative]
	public subtract(uint other) => (uint);
	
	// Method for CanMultiply<uint>
	/**
	 * Multiplies the current value by another {@code uint} and returns the resulting value.	 
	 * @param other value to multiply by
	 * @return product of the two values
	 */
	[ImportNative]
	public multiply(uint other) => (uint);
	
	// Method for CanDivide<uint>
	/**
	 * Divides the current value by another {@code uint} and returns the quotient.	 
	 * @param other value to divide by
	 * @return quotient of the division
	 */
	[ImportNative]
	public divide(uint other) => (uint);
	
	// Method for CanModulus<uint>
	/**
	 * Divides the current value by another {@code uint} and returns the remainder.	 
	 * @param other value to divide by
	 * @return remainder of the division
	 */
	[ImportNative]
	public modulus(uint other) => (uint);
	
	// Method for CanCompare<uint>
	/**
	 * Compares the current value to another {@code uint}, returning {@code -1},
	 * {@code 0}, or {@code 1}, if the current value is smaller than, equal to,
	 * or larger than the other value, respectively.
	 * @param other value to compare to
	 * @return whether smaller, equal, or larger
	 */	
	[ImportNative]
	public compare(uint other) => (int);
	
	// Method for CanEqual<uint>
	/**
	 * Compares the current value to another {@code uint}, returning {@code true}
	 * if they represent the same number.
	 * @param other value to compare to
	 * @return {@code true} if identical
	 */
	[ImportNative]
	public equal(uint other) => (boolean);

	// Methods for Integer<ulong>
	
	/** 
	 * Computes the bitwise OR of the value and a {@code ulong} value.
	 * @param other value to bitwise OR with
	 * @return result of bitwise OR 
	 */
	public bitOr(ulong other) => (ulong)
	{
		return cast<ulong>(this) | other;
	}
	
	/** 
	 * Computes the bitwise XOR of the value and a {@code ulong} value.
	 * @param other value to bitwise XOR with
	 * @return result of bitwise XOR 
	 */
	public bitXor(ulong other) => (ulong)
	{
		return cast<ulong>(this) ^ other;
	}
	
	/** 
	 * Computes the bitwise AND of the value and a {@code ulong} value.
	 * @param other value to bitwise AND with
	 * @return result of bitwise AND 
	 */
	public bitAnd(ulong other) => (ulong)
	{
		return cast<ulong>(this) & other;
	}	
	
	// Method for CanAdd<ulong>
	/**
	 * Adds a {@code ulong} to the current value and returns the resulting value.	 
	 * @param other value to add
	 * @return sum of the two values
	 */
	public add(ulong other) => (ulong)
	{
		return cast<ulong>(this) + other;
	}
	
	// Method for CanSubtract<ulong>
	/**
	 * Subtracts a {@code ulong} from the current value and returns the resulting value.	 
	 * @param other value to subtract
	 * @return difference of the two values
	 */
	public subtract(ulong other) => (ulong)
	{
		return cast<ulong>(this) - other;
	}	
	 
	 // Method for CanMultiply<ulong>
	/**
	 * Multiplies the current value by a {@code ulong} and returns the resulting value.	 
	 * @param other value to multiply by
	 * @return product of the two values
	 */
	public multiply(ulong other) => (ulong)
	{
		return cast<ulong>(this) * other;
	}
	
	// Method for CanDivide<ulong>
	/**
	 * Divides the current value by a {@code ulong} and returns the quotient.	 
	 * @param other value to divide by
	 * @return quotient of the division
	 */
	public divide(ulong other) => (ulong)
	{
		return cast<ulong>(this) / other;
	}
	
	// Method for CanModulus<ulong>
	/**
	 * Divides the current value by a {@code ulong} and returns the remainder.	 
	 * @param other value to divide by
	 * @return remainder of the division
	 */
	public modulus(ulong other) => (ulong)
	{
		return cast<ulong>(this) % other;
	}
	
	// Method for CanCompare<ulong>
	/**
	 * Compares the current value to a {@code ulong}, returning {@code -1},
	 * {@code 0}, or {@code 1}, if the current value is smaller than, equal to, or larger
	 * than the other value, respectively.
	 * @param other value to compare to
	 * @return whether smaller, equal, or larger
	 */
	public compare(ulong other) => (int)
	{
		return cast<ulong>(this).compare(other);
	}
	
	// Method for CanEqual<ulong>
	/**
	 * Compares the current value to a {@code ulong}, returning {@code true}
	 * if they represent the same number.
	 * @param other value to compare to
	 * @return {@code true} if identical
	 */
	public equal(ulong other) => (boolean)
	{
		return cast<ulong>(this) == other;
	}
	
	// Methods for Integer<long>
	
	/** 
	 * Computes the bitwise OR of the value and a {@code long} value.
	 * @param other value to bitwise OR with
	 * @return result of bitwise OR 
	 */
	public bitOr(long other) => (long)
	{
		return cast<long>(this) | other;
	}
	
	/** 
	 * Computes the bitwise XOR of the value and a {@code long} value.
	 * @param other value to bitwise XOR with
	 * @return result of bitwise XOR 
	 */
	public bitXor(long other) => (long)
	{
		return cast<long>(this) ^ other;
	}
	
	/** 
	 * Computes the bitwise AND of the value and a {@code long} value.
	 * @param other value to bitwise AND with
	 * @return result of bitwise AND 
	 */
	public bitAnd(long other) => (long)
	{
		return cast<long>(this) & other;
	}
	
	// Method for CanAdd<long>
	/**
	 * Adds a {@code long} to the current value and returns the resulting value.	 
	 * @param other value to add
	 * @return sum of the two values
	 */
	public add(long other) => (long)
	{
		return cast<long>(this) + other;
	}
	
	// Method for CanSubtract<long>
	/**
	 * Subtracts a {@code long} from the current value and returns the resulting value.	 
	 * @param other value to subtract
	 * @return difference of the two values
	 */
	public subtract(long other) => (long)
	{
		return cast<long>(this) - other;
	}
	
	// Method for CanMultiply<long>
	/**
	 * Multiplies the current value by a {@code long} and returns the resulting value.	 
	 * @param other value to multiply by
	 * @return product of the two values
	 */
	public multiply(long other) => (long)
	{
		return cast<long>(this) * other;
	}
	
	// Method for CanDivide<long>
	/**
	 * Divides the current value by a {@code long} and returns the quotient.	 
	 * @param other value to divide by
	 * @return quotient of the division
	 */
	public divide(long other) => (long)
	{
		return cast<long>(this) / other;
	}
	
	// Method for CanModulus<long>
	/**
	 * Divides the current value by a {@code long} and returns the remainder.	 
	 * @param other value to divide by
	 * @return remainder of the division
	 */
	public modulus(long other) => (long)
	{
		return cast<long>(this) % other;
	}
	
	// Method for CanCompare<long>
	/**
	 * Compares the current value to a {@code long}, returning {@code -1},
	 * {@code 0}, or {@code 1}, if the current value is smaller than, equal to, or larger
	 * than the other value, respectively.
	 * @param other value to compare to
	 * @return whether smaller, equal, or larger
	 */
	public compare(long other) => (int)
	{
		return cast<long>(this).compare(other);
	}
	
	// Method for CanEqual<long>
	/**
	 * Compares the current value to a {@code long}, returning {@code true}
	 * if they represent the same number.
	 * @param other value to compare to
	 * @return {@code true} if identical
	 */
	public equal(long other) => (boolean)
	{
		return cast<long>(this) == other;
	}
	

	// Method for CanAdd<float>
	/**
	 * Adds a {@code float} to the current value and returns the resulting value.	 
	 * @param other value to add
	 * @return sum of the two values
	 */
	public add(float other) => (float)
	{
		return cast<float>(this) + other;
	}
	
	// Method for CanSubtract<float>
	/**
	 * Subtracts a {@code float} from the current value and returns the resulting value.	 
	 * @param other value to subtract
	 * @return difference of the two values
	 */
	public subtract(float other) => (float)
	{
		return cast<float>(this) - other;
	}
	
	// Method for CanMultiply<float>
	/**
	 * Multiplies the current value by a {@code float} and returns the resulting value.	 
	 * @param other value to multiply by
	 * @return product of the two values
	 */
	public multiply(float other) => (float)
	{
		return cast<float>(this) * other;
	}
	
	// Method for CanDivide<float>
	/**
	 * Divides the current value by a {@code float} and returns the quotient.	 
	 * @param other value to divide by
	 * @return quotient of the division
	 */
	public divide(float other) => (float)
	{
		return cast<float>(this) / other;
	}
	
	// Method for CanModulus<float>
	/**
	 * Divides the current value by a {@code float} and returns the remainder.	 
	 * @param other value to divide by
	 * @return remainder of the division
	 */
	public modulus(float other) => (float)
	{
		return cast<float>(this) % other;
	}
	
	// Method for CanCompare<float>	
	/**
	 * Compares the current value to a {@code float}, returning {@code -1},
	 * {@code 0}, or {@code 1}, if the current value is smaller than, equal to, or larger
	 * than the other value, respectively.
	 * @param other value to compare to
	 * @return whether smaller, equal, or larger
	 */
	public compare(float other) => (int)
	{
		return cast<float>(this).compare(other);
	}
	
	// Method for CanEqual<float>
	/**
	 * Compares the current value to a {@code float}, returning {@code true}
	 * if they represent the same number.
	 * @param other value to compare to
	 * @return {@code true} if identical
	 */
	public equal(float other) => (boolean)
	{
		return cast<float>(this) == other;
	}

	// Method for CanAdd<double>
	/**
	 * Adds a {@code double} to the current value and returns the resulting value.	 
	 * @param other value to add
	 * @return sum of the two values
	 */
	public add(double other) => (double)
	{
		return cast<double>(this) + other;
	}
	
	// Method for CanSubtract<double>
	/**
	 * Subtracts a {@code double} from the current value and returns the resulting value.	 
	 * @param other value to subtract
	 * @return difference of the two values
	 */
	public subtract(double other) => (double)
	{
		return cast<double>(this) - other;
	}
	
	// Method for CanMultiply<double>
	/**
	 * Multiplies the current value by a {@code double} and returns the resulting value.	 
	 * @param other value to multiply by
	 * @return product of the two values
	 */
	public multiply(double other) => (double)
	{
		return cast<double>(this) * other;
	}
	
	// Method for CanDivide<double>
	/**
	 * Divides the current value by a {@code double} and returns the quotient.	 
	 * @param other value to divide by
	 * @return quotient of the division
	 */
	public divide(double other) => (double)
	{
		return cast<double>(this) / other;
	}
	
	// Method for CanModulus<double>
	/**
	 * Divides the current value by a {@code double} and returns the remainder.	 
	 * @param other value to divide by
	 * @return remainder of the division
	 */
	public modulus(double other) => (double)
	{
		return cast<double>(this) % other;
	}
	
	// Method for CanCompare<double>
	/**
	 * Compares the current value to a {@code double}, returning {@code -1},
	 * {@code 0}, or {@code 1}, if the current value is smaller than, equal to, or larger
	 * than the other value, respectively.
	 * @param other value to compare to
	 * @return whether smaller, equal, or larger
	 */
	public compare(double other) => (int)
	{
		return cast<double>(this).compare(other);
	}
	
	// Method for CanEqual<double>
	/**
	 * Compares the current value to a {@code double}, returning {@code true}
	 * if they represent the same number.
	 * @param other value to compare to
	 * @return {@code true} if identical
	 */
	public equal(double other) => (boolean)
	{
		return cast<double>(this) == other;
	}
	
	/** 
	 * Finds the square root of the value.
	 * @return square root	 
	 */
	public squareRoot() => (double)
	{
		return toDouble().squareRoot();
	}
	
	/** 
	 * Raises the current value to the power of an {@code int}.
	 * @param exponent power to raise by	 
	 * @return number after exponentiation
	 */
	public power(int value) => (double)
	{
		return toDouble().power(value);
	}
	
	/** 
	 * Raises the current value to the power of a {@code double}.
	 * @param exponent power to raise by	 
	 * @return number after exponentiation
	 */
	public power(double value) => (double)
	{
		return toDouble().power(value);
	}
	
	/** 
	 * Finds the sine of the current value.	 	 
	 * @return sine of the value
	 */
	public sin() => (double)
	{
		return toDouble().sin();
	}
	
	/** 
	 * Finds the cosine of the current value.	 	 
	 * @return cosine of the value
	 */
	public cos() => (double)	
	{
		return toDouble().cos();
	}
	
	/** 
	 * Finds the tangent of the current value.	 	 
	 * @return tangent of the value
	 */
	public tan() => (double)
	{
		return toDouble().tan();
	}
	
	/** 
	 * Finds the natural logarithm of the current value.	 	 
	 * @return natural logarithm of the value
	 */
	public logBaseE() => (double)	
	{
		return toDouble().logBaseE();
	}
	
	/** 
	 * Finds the logarithm base 2 of the current value.	 	 
	 * @return logarithm base 2 of the value
	 */
	public logBase2() => (double)	
	{
		return toDouble().logBase2();
	}
	
	/** 
	 * Finds the logarithm base 10 of the current value.	 	 
	 * @return logarithm base 10 of the value
	 */
	public logBase10() => (double)	
	{
		return toDouble().logBase10();
	}
	
	/** 
	 * Produces a version of the value with endianness reversed. In other
	 * words, the order of the 4 bytes has been reversed.	 	 
	 * @return value with opposite endianness
	 */	 
	[ImportNative]
	public flipEndian() => (uint);
	
	/** 
	 * Counts the number of 1 bits in the binary representation of the number.	 	 
	 * @return total 1 bits
	 */	
	[ImportNative]
	public ones() => (uint);
	
	/** 
	 * Counts the number of 0 bits before a non-zero bit, starting with the
	 * most significant bits. 	 
	 * @return leading 0 bits
	 */
	[ImportNative]
	public leadingZeroes() => (uint);
	
	/** 
	 * Counts the number of 0 bits after a non-zero bit, starting with the
	 * least significant bits. 	 
	 * @return trailing 0 bits
	 */
	[ImportNative]
	public trailingZeroes() => (uint);
	
	/**
	 * Adds another {@code uint} to the current value and returns the resulting
	 * value and whether or not overflow occurred.	 
	 * @param other value to add
	 * @return sum of the two values
	 * @return {@code true} if overflow occurred
	 */
	[ImportNative]
	public addWithOverflow(uint value) => (uint, boolean);
	
	/**
	 * Subtracts another {@code uint} from the current value and returns the resulting
	 * value and whether or not overflow occurred.	 
	 * @param other value to subtract
	 * @return difference of the two values
	 * @return {@code true} if overflow occurred
	 */
	[ImportNative]
	public subtractWithOverflow(uint value) => (uint, boolean);
	
	/**
	 * Multiplies the current value by another {@code uint} and returns the
	 * resulting value and whether or not overflow occurred.	 
	 * @param other value to multiply by
	 * @return product of the two values
	 * @return {@code true} if overflow occurred
	 */
	[ImportNative]
	public multiplyWithOverflow(uint value) => (uint, boolean);

	// Method for CanHash
	/** 
	 * Finds a hash value for the current value.	 
	 * @return hash value	 
	 */
	public get hash() => (ulong)
	{
		return cast<ulong>(this);
	}

	/** 
	 * Finds the minimum of the current value and another {@code uint}.
	 * @param other value
	 * @return minimum value	 
	 */
	public min(uint other) => (uint)
	{
		return this < other ? this, other;
	}
	
	/** 
	 * Finds the maximum of the current value and another {@code uint}.
	 * @param other value
	 * @return maximum value	 
	 */
	public max(uint other) => (uint)
	{
		return this > other ? this, other;
	}

	/**
	 * Returns a signed version of the current value, overflowing if too large.
	 * @return signed value	  
	 */
	public toSigned() => (int)
	{
		return toInt();
	}
	
	/** 
	 * Returns a {@code String} representation of the value, in base 10.
	 * @return  {@code String} representation
	 */ 
	public toString() => (String)
	{
		return toString(10u);
	}
	
	/** 
	 * Returns a {@code String} representation of the value, in the specified base.
	 * @param base base of output representation
	 * @return  {@code String} representation
	 */
	public toString(uint base) => (String)
	{	
		MutableString string = MutableString:create();
		
		uint value = this;
		do
		{			
			string.append(digit(value % base));			
			value /= base;		
		}
		while (value != 0u);
	
		return string.reverse().toString();
	}
	
	// Converts a digit to its byte value, assuming digits between 0 and 35.	
	private digit(uint digit) => (ubyte)
	{
		if (digit < 10u)
			return cast<ubyte>(digit + '0');
		return cast<ubyte>(digit - 10u + 'a');
	}
	
	// C FRAMEWORK EXTENSIONS
	[ExportAssembly]
	private shadowUInt_GetClass() => (Class)
	{
		return uint:class;
	}
}
