/**
 * Class {@code ulong} contains the methods that can be called on {@code ulong}  
 * values.
 */
locked immutable class shadow:standard@
	ULong
is	Integer<ulong>
and	Number<double>
and	CanHash
{
	/// Largest value a {@code ulong} can have, 18446744073709551615.
	public constant ulong MAX = 18446744073709551615uL;
	/// Smallest value a {@code ulong} can have, 0.
	public constant ulong MIN = 0uL;
	
	// Methods for Integer<ulong>
	
	/**
	 * Returns the bitwise complement of the {@code ulong} value.
	 * @return bitwise complement 
	 */ 
	[ImportNative]
	public bitComplement() => (ulong);
	
	/** 
	 * Finds the result of left-shifting the bits of the {@code ulong} value
	 * by an unsigned amount.
	 * @param amount bits to shift
	 * @return shifted value 
	 */
	[ImportNative]
	public bitShiftLeft(uint amount) => (ulong);
	
	/** 
	 * Finds the result of right-shifting the bits of the {@code ulong} value
	 * by an unsigned amount.
	 * @param amount bits to shift
	 * @return shifted value 
	 */
	[ImportNative]
	public bitShiftRight(uint amount) => (ulong);
	
	/** 
	 * Finds the result of left-rotating the bits of the {@code ulong} value
	 * by an unsigned amount.
	 * @param amount bits to rotate
	 * @return rotated value 
	 */
	[ImportNative]
	public bitRotateLeft(uint amount) => (ulong);
	
	/** 
	 * Finds the result of right-rotating the bits of the {@code ulong} value
	 * by an unsigned amount.
	 * @param amount bits to rotate
	 * @return rotated value 
	 */
	[ImportNative]
	public bitRotateRight(uint amount) => (ulong);
	
	/** 
	 * Finds the result of left-shifting the bits of the {@code ulong} value
	 * by a signed amount which may be positive, negative, or zero.
	 * @param amount bits to shift
	 * @return shifted value 
	 */
	public bitShiftLeft(int amount) => (ulong)
	{
		uint abs = amount.abs();
		return amount >= 0 ? bitShiftLeft(abs), bitShiftRight(abs);
	}
	
	/** 
	 * Finds the result of right-shifting the bits of the {@code ulong} value
	 * by a signed amount which may be positive, negative, or zero.
	 * @param amount bits to shift
	 * @return shifted value 
	 */
	public bitShiftRight(int amount) => (ulong)
	{
		uint abs = amount.abs();
		return amount >= 0 ? bitShiftRight(abs), bitShiftLeft(abs);
	}
	
	/** 
	 * Finds the result of left-rotating the bits of the {@code ulong} value
	 * by a signed amount which may be positive, negative, or zero.
	 * @param amount bits to rotate
	 * @return rotated value 
	 */
	public bitRotateLeft(int amount) => (ulong)
	{
		uint abs = amount.abs();
		return amount >= 0 ? bitRotateLeft(abs), bitRotateRight(abs);
	}
	
	/** 
	 * Finds the result of right-rotating the bits of the {@code ulong} value
	 * by a signed amount which may be positive, negative, or zero.
	 * @param amount bits to rotate
	 * @return rotated value 
	 */
	public bitRotateRight(int amount) => (ulong)
	{
		uint abs = amount.abs();
		return amount >= 0 ? bitRotateRight(abs), bitRotateLeft(abs);
	}
	
	// Methods for Number
	
	/** 
	 * Converts the {@code ulong} value to a {@code byte}, which will overflow
	 * if the value is too large to be contained in a {@code byte}.	 
	 * @return {@code byte} value 
	 */
	[ImportNative]
	public toByte() => (byte);
	
	/** 
	 * Converts the {@code ulong} value to a {@code ubyte}, which will overflow
	 * if the value is too large to be contained in a {@code ubyte}.	 
	 * @return {@code ubyte} value 
	 */
	[ImportNative]
	public toUByte() => (ubyte);
	
	/** 
	 * Converts the {@code ulong} value to a {@code short}, which will overflow
	 * if the value is too large to be contained in a {@code short}.
	 * @return {@code short} value 
	 */
	[ImportNative]
	public toShort() => (short);
	
	/** 
	 * Converts the {@code ulong} value to a {@code ushort}, which will overflow
	 * if the value is too large to be contained in a {@code ushort}.
	 * @return {@code ushort} value 
	 */
	[ImportNative]
	public toUShort() => (ushort);
	
	/** 
	 * Converts the {@code ulong} value to an {@code int}, which will overflow
	 * if the value is too large to be contained in an {@code int}.
	 * @return {@code int} value 
	 */
	[ImportNative]
	public toInt() => (int);
	
	/** 
	 * Converts the {@code ulong} value to a {@code uint}, which will overflow
	 * if the value is too large to be contained in a {@code uint}.
	 * @return {@code uint} value 
	 */
	[ImportNative]
	public toUInt() => (uint);
	
	/** 
	 * Converts the {@code ulong} value to a {@code long}, which will overflow
	 * if the value is too large to be contained in a {@code long}.
	 * @return {@code uint} value 
	 */
	[ImportNative]
	public toLong() => (long);
	
	/** 
	 * Converts the {@code ulong} value to a {@code ulong}, which involves
	 * no change.
	 * @return {@code ulong} value 
	 */
	[ImportNative]
	public toULong() => (ulong);
	
	/** 
	 * Converts the {@code ulong} value to a {@code code}, which will overflow
	 * if the value is too large to be contained in a {@code code}.
	 * @return {@code code} value 
	 */
	[ImportNative]
	public toCode() => (code);
	
	/** 
	 * Converts the {@code ulong} value to a {@code float}.
	 * @return {@code float} value 
	 */
	[ImportNative]
	public toFloat() => (float);
	
	/** 
	 * Converts the {@code ulong} value to a {@code double}.
	 * @return {@code double} value 
	 */
	[ImportNative]
	public toDouble() => (double);

	// Methods for Integer<ulong>
	
	/** 
	 * Computes the bitwise OR of the value and another {@code ulong} value.
	 * @param other value to bitwise OR with
	 * @return result of bitwise OR 
	 */
	[ImportNative]
	public bitOr(ulong other) => (ulong);
	
	/** 
	 * Computes the bitwise XOR of the value and another {@code ulong} value.
	 * @param other value to bitwise XOR with
	 * @return result of bitwise XOR 
	 */
	[ImportNative]
	public bitXor(ulong other) => (ulong);
	
	/** 
	 * Computes the bitwise AND of the value and another {@code ulong} value.
	 * @param other value to bitwise AND with
	 * @return result of bitwise AND
	 */
	[ImportNative]
	public bitAnd(ulong other) => (ulong);
	
	
	// Method for CanAdd<ulong>
	/**
	 * Adds another {@code ulong} to the current value and returns the resulting value.	 
	 * @param other value to add
	 * @return sum of the two values
	 */
	[ImportNative]
	public add(ulong other) => (ulong);
	
	// Method for CanSubtract<ulong>
	/**
	 * Subtracts another {@code ulong} from the current value and returns the resulting value.	 
	 * @param other value to subtract
	 * @return difference of the two values
	 */
	[ImportNative]
	public subtract(ulong other) => (ulong);
	
	// Method for CanMultiply<ulong>
	/**
	 * Multiplies the current value by another {@code ulong} and returns the resulting value.	 
	 * @param other value to multiply by
	 * @return product of the two values
	 */
	[ImportNative]
	public multiply(ulong other) => (ulong);
	
	// Method for CanDivide<ulong>
	/**
	 * Divides the current value by another {@code ulong} and returns the quotient.	 
	 * @param other value to divide by
	 * @return quotient of the division
	 */
	[ImportNative]
	public divide(ulong other) => (ulong);
	
	// Method for CanModulus<ulong>
	/**
	 * Divides the current value by another {@code ulong} and returns the remainder.	 
	 * @param other value to divide by
	 * @return remainder of the division
	 */
	[ImportNative]
	public modulus(ulong other) => (ulong);
	
	// Method for CanCompare<ulong>
	/**
	 * Compares the current value to another {@code ulong}, returning {@code -1},
	 * {@code 0}, or {@code 1}, if the current value is smaller than, equal to,
	 * or larger than the other value, respectively.
	 * @param other value to compare to
	 * @return whether smaller, equal, or larger
	 */	
	[ImportNative]
	public compare(ulong other) => (int);
	
	// Method for CanEqual<ulong>
	/**
	 * Compares the current value to another {@code ulong}, returning {@code true}
	 * if they represent the same number.
	 * @param other value to compare to
	 * @return {@code true} if identical
	 */
	[ImportNative]
	public equal(ulong other) => (boolean);

	// Method for CanAdd<double>
	/**
	 * Adds a {@code double} to the current value and returns the resulting value.	 
	 * @param other value to add
	 * @return sum of the two values
	 */
	public add(double other) => (double)
	{
		return cast<double>(this) + other;
	}
	
	// Method for CanSubtract<double>
	/**
	 * Subtracts a {@code double} from the current value and returns the resulting value.	 
	 * @param other value to subtract
	 * @return difference of the two values
	 */
	public subtract(double other) => (double)
	{
		return cast<double>(this) - other;
	}
	
	// Method for CanMultiply<double>
	/**
	 * Multiplies the current value by a {@code double} and returns the resulting value.	 
	 * @param other value to multiply by
	 * @return product of the two values
	 */
	public multiply(double other) => (double)
	{
		return cast<double>(this) * other;
	}
	
	// Method for CanDivide<double>
	/**
	 * Divides the current value by a {@code double} and returns the quotient.	 
	 * @param other value to divide by
	 * @return quotient of the division
	 */
	public divide(double other) => (double)
	{
		return cast<double>(this) / other;
	}
	
	// Method for CanModulus<double>
	/**
	 * Divides the current value by a {@code double} and returns the remainder.	 
	 * @param other value to divide by
	 * @return remainder of the division
	 */
	public modulus(double other) => (double)
	{
		return cast<double>(this) % other;
	}
	
	// Method for CanCompare<double>
	/**
	 * Compares the current value to a {@code double}, returning {@code -1},
	 * {@code 0}, or {@code 1}, if the current value is smaller than, equal to, or larger
	 * than the other value, respectively.
	 * @param other value to compare to
	 * @return whether smaller, equal, or larger
	 */
	public compare(double other) => (int)
	{
		return cast<double>(this).compare(other);
	}
	
	// Method for CanEqual<double>
	/**
	 * Compares the current value to a {@code double}, returning {@code true}
	 * if they represent the same number.
	 * @param other value to compare to
	 * @return {@code true} if identical
	 */
	public equal(double other) => (boolean)
	{
		return cast<double>(this) == other;
	}
	
	/** 
	 * Finds the square root of the value.
	 * @return square root	 
	 */
	public squareRoot() => (double)
	{
		return toDouble().squareRoot();
	}
	
	/** 
	 * Raises the current value to the power of an {@code int}.
	 * @param exponent power to raise by	 
	 * @return number after exponentiation
	 */
	public power(int value) => (double)
	{
		return toDouble().power(value);
	}
	
	/** 
	 * Raises the current value to the power of a {@code double}.
	 * @param exponent power to raise by	 
	 * @return number after exponentiation
	 */
	public power(double value) => (double)
	{
		return toDouble().power(value);
	}
	
	/** 
	 * Finds the sine of the current value.	 	 
	 * @return sine of the value
	 */
	public sin() => (double)
	{
		return toDouble().sin();
	}
	
	/** 
	 * Finds the cosine of the current value.	 	 
	 * @return cosine of the value
	 */
	public cos() => (double)	
	{
		return toDouble().cos();
	}
	
	/** 
	 * Finds the natural logarithm of the current value.	 	 
	 * @return natural logarithm of the value
	 */
	public logBaseE() => (double)	
	{
		return toDouble().logBaseE();
	}
	
	/** 
	 * Finds the logarithm base 2 of the current value.	 	 
	 * @return logarithm base 2 of the value
	 */
	public logBase2() => (double)	
	{
		return toDouble().logBase2();
	}
	
	/** 
	 * Finds the logarithm base 10 of the current value.	 	 
	 * @return logarithm base 10 of the value
	 */
	public logBase10() => (double)	
	{
		return toDouble().logBase10();
	}
	
	/** 
	 * Produces a version of the value with endianness reversed. In other
	 * words, the order of the 8 bytes has been reversed.	 	 
	 * @return value with opposite endianness
	 */
	[ImportNative]
	public flipEndian() => (ulong);
	
	/** 
	 * Counts the number of 1 bits in the binary representation of the number.	 	 
	 * @return total 1 bits
	 */	
	[ImportNative]
	public ones() => (ulong);
	
	/** 
	 * Counts the number of 0 bits before a non-zero bit, starting with the
	 * most significant bits. 	 
	 * @return leading 0 bits
	 */
	[ImportNative]
	public leadingZeroes() => (ulong);
	
	/** 
	 * Counts the number of 0 bits after a non-zero bit, starting with the
	 * least significant bits. 	 
	 * @return trailing 0 bits
	 */
	[ImportNative]
	public trailingZeroes() => (ulong);	
	

	// Method for CanHash
	/** 
	 * Finds a hash value for the current value.	 
	 * @return hash value	 
	 */
	public get hash() => (ulong)
	{
		return this;
	}

	/** 
	 * Finds the minimum of the current value and another {@code ulong}.
	 * @param other value
	 * @return minimum value	 
	 */
	public min(ulong other) => (ulong)
	{
		return this < other ? this, other;
	}
	
	/** 
	 * Finds the maximum of the current value and another {@code ulong}.
	 * @param other value
	 * @return maximum value	 
	 */
	public max(ulong other) => (ulong)
	{
		return this > other ? this, other;
	}

	/**
	 * Returns a signed version of the current value, overflowing if too large.
	 * @return signed value	  
	 */
	public toSigned() => (long)
	{
		return toLong();
	}
	
	/** 
	 * Returns a {@code String} representation of the value, in base 10.
	 * @return  {@code String} representation
	 */
	public toString() => (String)
	{
		return toString(10u);
	}
	
	/** 
	 * Returns a {@code String} representation of the value, in the specified base.
	 * @param base base of output representation
	 * @return  {@code String} representation
	 */
	public toString(uint base) => (String)
	{
		MutableString string = MutableString:create();
		ulong longBase = base; // casts to ulong a single time
		ulong value = this;
		do
		{
			string.append(digit(value % longBase));
			value /= longBase;
		}
		while (value != 0uL);
		return string.reverse().toString();
	}
	
	// Converts a digit to its byte value, assuming digits between 0 and 35.
	private digit(ulong digit) => (code)
	{
		if (digit < 10uL)
			return cast<code>(digit + '0');
		return cast<code>(digit - 10uL + 'a');
	}
	
	// C FRAMEWORK EXTENSIONS
	[ExportAssembly]
	private shadowULong_GetClass() => (Class)
	{
		return ulong:class;
	}
}
