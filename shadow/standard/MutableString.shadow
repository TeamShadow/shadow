/**
 * Class {@code MutableString} holds arbitrary-length
 * sequences of UTF-8 code points stored in a dynamic array of {@code byte}
 * values. {@code MutableString} objects differ from {@code String} objects
 * in that their contents can be changed.
 *
 * @seeDoc shadow:standard@String 
 */
locked class shadow:standard@
	MutableString
is  CanIndex<long, byte>
and CanIndexStore<long, byte>
and CanCompare<MutableString>
and CanHash
{
	/// Number of {@code byte} values stored as part of the string. 
	get long sizeLong = 0L;
	byte[] data;
	
	/**
	 * Creates a new {@code MutableString} with no code points and the
	 * capacity to hold 10 bytes worth of code points. 
	 */
	public create()
	{
		this(10L);
	}
	
	/**
	 * Creates a new {@code MutableString} with no code points and the
	 * capacity to hold the specified number of bytes worth of code points.
	 * @param capacity initial storage capacity in bytes  
	 */
	public create( long capacity )
	{
		data = byte:create[capacity];
	}
	
	/**
	 * Creates a new {@code MutableString} containing the string
	 * representation of an object.
	 * @param initialValue object whose string representation will fill the
	 * new {@code MutableString} 
	 */
	public create( nullable Object initialValue )
	{
		String value = #initialValue;
		data = byte:create[value->sizeLong];	
		append(value);
	}
	
	/**
	 * Gets the number of {@code byte} values stored as part of the string as an {@code int}.	 
	 * @return number of bytes	 
	 */
	public get size() => ( int )
	{
		return cast<int>(sizeLong);
	}
	
	
	/**
	 * Guarantees that there is enough space in the {@code MutableString} to
	 * hold the specified number of bytes.  If there is insufficient space,
	 * this method will take the larger of the requested size and twice the
	 * current capacity, to avoid repeated resizes for a series of single byte
	 * additions.	 	 
	 * @param size minimize number of bytes the {@code MutableString} must be
	 * able to hold  
	 * @return current {@code MutableString} object, possibly resized 
	 */
	public ensureCapacity( long size ) => ( MutableString )
	{
		if ( this->capacity < sizeLong )
		{
			sizeLong = sizeLong.max( this->capacity * 2L );
			byte[] data = byte:create[sizeLong];
			for ( long i = 0; i < this->sizeLong; i += 1L )
				data[i] = this:data[i];
			this:data = data;
		}
		return this;
	}
	
	/**
	 * Retrieves the current capacity of the {@code MutableString} in bytes.	   
	 * @return current capacity in bytes 
	 */
	public readonly get capacity() => ( long )
	{
		return data->sizeLong;
	}

	/**
	 * Retrieves the {@code byte} value at the given offset inside the object.
	 * @param index offset inside the object	   
	 * @return {@code byte} value
	 * @throws IndexOutOfBoundsException if the offset is illegal
	 */
	public readonly index( long index ) => ( byte value )
	{
		return data[index];
	}
	
	/**
	 * Stores the given {@code byte} value at the given offset inside the
	 * object.
	 * @param index offset inside the object	   
	 * @param value value to store	 
	 * @throws IndexOutOfBoundsException if the offset is illegal
	 */
	public index( long index, byte value ) => ()
	{
		data[index] = value;
	}
	
	/**
	 * Inserts the given {@code byte} value at the given offset inside the
	 * object, moving over any values coming after the offset
	 * @param index offset inside the object
	 * @param value value to store	 
	 * @throws IndexOutOfBoundsException if the offset is illegal
	 */
	public insert( long index, byte value ) => (MutableString)
	{
		ensureCapacity( sizeLong + 1 );
		for( long i = sizeLong; i >= index + 1L; i -= 1L )
			data[i] = data[i - 1L];		
		data[index] = value;
		sizeLong += 1L;
		return this;			
	}
	
	/**
	 * Inserts a {@code String} representation of the given value at the given
	 * offset inside the object, moving over any values coming after the
	 * offset
	 * @param index offset inside the object
	 * @param value value to store
	 * @return {@code MutableString} object after insertion	 
	 * @throws IndexOutOfBoundsException if the offset is illegal
	 */
	public insert( long index, nullable Object value ) => (MutableString)
	{
		String string = #value;
		ensureCapacity( sizeLong + string->sizeLong );
		for( long i = sizeLong + string->sizeLong - 1L; i >= index + string->sizeLong; i -= 1L )
			data[i] = data[i - string->sizeLong];
		for( long i = 0L; i < string->sizeLong; i += 1L )
			data[i + index] = string.index(i);
		sizeLong += string->sizeLong;
		return this;			
	}
	
	/**
	 * Deletes the byte value at the given offset, shortening the string
	 * representation by one byte.
	 * @param index offset inside the object	
	 * @return {@code MutableString} object after deletion 	 
	 * @throws IndexOutOfBoundsException if the offset is illegal
	 */
	public delete(long index) => (MutableString)
	{
		try
		{
			return delete(index, index + 1L);
		}
		catch(IllegalArgumentException e)
		{
			throw IndexOutOfBoundsException:create(index);
		}
	}
	
	/**
	 * Deletes all the byte values from the starting offset up to but not
	 * including the ending offset, shortening the string representation by
	 * {@code end - start} bytes.
	 * @param start starting offset
	 * @param end ending offset	
	 * @return {@code MutableString} object after deletion 	 
	 * @throws IllegalArgumentException if either of the offsets is illegal
	 */
	public delete(long start, long end) => (MutableString)
	{
		if ( start < 0L or start >= sizeLong )
			throw IllegalArgumentException:create("Invalid start index");
			
		if( end < start or end > sizeLong )
			throw IllegalArgumentException:create("Invalid end index");
			
		for( long i = start; i < sizeLong - (end - start); i += 1L )
			data[i] = data[i + (end - start)];
		
		sizeLong -= end - start;
		
		return this;
	}
	
	/**
	 * Appends a single {@code byte} value to the end of the string
	 * representation.	 
	 * @param value value to append	 
	 * @return {@code MutableString} object after append
	 */	
	public append( byte value ) => ( MutableString )
	{		
		ensureCapacity( sizeLong + 1L );		
		data[sizeLong] = value;
		sizeLong += 1L;
		return this;
	}
	
	/**
	 * Appends a {@code String} representation of the given value to the end
	 * of the string representation.	 
	 * @param value value to append	 
	 * @return {@code MutableString} object after append
	 */
	public append( nullable Object value ) => ( MutableString )
	{		
		String string = #value;		
		ensureCapacity( sizeLong + string->sizeLong );
		for ( long i = 0L; i < string->sizeLong; i += 1L, sizeLong += 1L )
			data[sizeLong] = string.index(i);
		return this;
	}
	
	/**
	 * Prepends a single {@code byte} value to the beginning of the string
	 * representation, moving all other values down.	 
	 * @param value value to prepend	 
	 * @return {@code MutableString} object after prepend
	 */
	public prepend( byte value ) => ( MutableString )
	{		
		return insert( 0L, value );
	}
	
	/**
	 * Prepends a {@code String} representation of the given value to the
	 * beginning of the string representation, moving all other values down.	 
	 * @param value value to append	 
	 * @return {@code MutableString} object after append
	 */
	public prepend( nullable Object value ) => ( MutableString )
	{
		String string = #value;
		ensureCapacity( sizeLong + string->sizeLong );
		for ( long i = 0L; i < sizeLong; i += 1L )		
			data[i + string->sizeLong] = data[i];
		for( long i = 0L; i < string->sizeLong; i += 1L )
			data[i] = string.index(i);
		sizeLong += string->sizeLong;
		return this;
	}
	
	/**
	 * Reverses the order of the {@code byte} values stored in this
	 * string representation.  Note that this method may not correctly reverse
	 * representations containing mult-byte code points.
	 * @return reversed {@code MutableString} object
	 */	
	public reverse() => ( MutableString )
	{
		for ( long left = 0, right = sizeLong - 1L; left < right;
				left += 1L, right -= 1L )
			( data[left], data[right] ) = ( data[right], data[left] );
		return this;
	}
	
	/**
	 * Produces a substring of this string representation, from the starting
	 * byte offset up to but not including the ending byte offset.
	 * @param start starting offset
	 * @param end ending offset	
	 * @return substring 	 
	 * @throws IllegalArgumentException if either of the offsets is illegal	 
	 */	
	public readonly substring(long start, long end) => (String)
	{
		if ( start < 0L or start >= sizeLong )
			throw IllegalArgumentException:create("Invalid start index");
			
		if( end < start or end > sizeLong )
			throw IllegalArgumentException:create("Invalid end index");
	
		return String:create(cast<byte[]>(data.subarray(start, end)));	
	}

	/**
	 * Produces an immutable {@code String} version of this string
	 * representation.	
	 * @return immutable {@code String} version
	 */
	public readonly toString() => ( String )
	{	
		byte[] bytes = data;

		if( data->sizeLong != sizeLong )			
			bytes = data.subarray(0L, sizeLong);		
		
		return String:create(bytes);
	}
	
	/**
	 * Compares the current object to another {@code MutableString},
	 * returning {@code true} if they are identical.
	 * @param other object to compare to
	 * @return {@code true} if identical
	 */
	public equal( MutableString other ) => ( boolean )
	{
		return sizeLong == other:sizeLong and compare(other) == 0;
	}
	
	/**
	 * Compares the current object to another {@code MutableString}, returning
	 * {@code -1}, {@code 0}, or {@code 1}, if the current object comes
	 * earlier, at exactly the same point, or later in a lexicographic
	 * ordering than the other value, respectively.
	 * @param other value to compare to
	 * @return whether earlier, identical, or later
	 */
	public compare( MutableString other ) => ( int )
	{
		for ( long i = 0L; i < sizeLong and i < other:sizeLong; i += 1L )
			if ( data[i] != other:data[i] )
				return data[i].compare(other:data[i]);
		return sizeLong.compare(other:sizeLong);
	}
	
	/** 
	 * Finds a hash value for the current object.	 
	 * @return hash value	 
	 */
	public get hash() => ( ulong )
	{
		return (#this)->hash;
	}
}