/**
 * Class {@code HashMap<K,V>} stores a map, also known as a symbol table,
 * of key-value pairs with entries mapping keys of type {@code K} to values
 * of type {@code V}. This map is implemented with a hash table that
 * employs the chaining strategy for collision resolution, allowing keys to
 * be added, found, and deleted in constant or near-constant time.
 * {@code HashMap<K,V>} requires that type {@code K} has the {@code CanHash}
 * interface but imposes no ordering requirement on keys.  The
 * {@code TreeMap<K,V>} class should be considered if key ordering is a
 * requirement.
 *
 * @seeDoc shadow:standard@CanHash
 * @seeDoc shadow:utility@TreeMap
 */
class shadow:utility@
	HashMap<K is CanHash and CanEqual<K>, V is CanEqual<V>>
is  Map<K,V>
{
	/// Default number of rows a hash table starts with, 16.	
	protected constant long DEFAULT_INITIAL_CAPACITY = 16L;
	/// Maximum rows the hash table can have, 1073741824.
	protected constant long MAXIMUM_CAPACITY = 1L << 30;
	/// Default load factor before the hash table is resized, 0.75.
	protected constant float DEFAULT_LOAD_FACTOR = 0.75f;
	
	/// Gets the number of key-value pairs in the map.
	get long sizeLong = 0L;
	nullable Node<K,V>[] table;
	float loadFactor;
	long threshold;
	long modifications = 0L;
	
	public readonly get size() => (int)
	{
		return cast<int>(sizeLong);
	}
	
	/**
	 * Creates an empty {@code HashMap} with a default capacity of 16
	 * and maximum load factor of 0.75.  
	 */	
	public create()
	{
		this(DEFAULT_INITIAL_CAPACITY);
	}
	
	/**
	 * Creates an empty {@code HashMap} with the specified capacity and
	 * maximum load factor. 
	 * @param initialCapacity initial capacity of the map
	 * @param loadFactor maximum load factor before the map is resized
	 */
	public create(long initialCapacity)
	{
		this(initialCapacity, DEFAULT_LOAD_FACTOR);
	}
	
	/**
	 * Creates a {@code HashMap} with the specified initial capacity of 
	 * buckets and the specified maximum load factor before all the
	 * key-value pairs are re-hashed.  Note that the capacity will always
	 * be increased to the next power of 2 if it is not a power of 2.
	 * @param initialCapacity initial number of buckets in the hash table
	 * @param loadFactor maximum load factor (buckets / keys) before all
	 * key-value pairs are re-hashed
	 */
	public create(long initialCapacity, float loadFactor)
	{
		long capacity = 1L;
		while( capacity < initialCapacity and capacity < MAXIMUM_CAPACITY)
			capacity <<= 1;
		table = Node<K,V>:null[capacity];
		this:loadFactor = loadFactor;
		threshold = cast<long>(capacity * loadFactor);
	}
	
	/*
	 * Resizes the map so that it can hold the new capacity, which must be a
	 * power of 2. 
	 * @param newCapacity new capacity of the map
	 */
	protected resize(long newCapacity) => ()
	{		
		if(table->sizeLong == MAXIMUM_CAPACITY)
		{
			threshold = long:MAX;
			return;
		}
		
		nullable var newTable = Node<K,V>:null[newCapacity];
		long index;
		long hash;		
		
		for( long i = 0L; i < table->sizeLong; i += 1L )
		{
			if( table[i] !== null )
			{ 
				nullable var next = table[i];
				while( next !== null )
				{
					var node = check(next);
					(index, hash) = findIndex( node->key, newCapacity );				
					newTable[index] = Node<K,V>:create(hash, node->key, node->value, newTable[index]);
					next = node->next;
				}
			}
		}
		
		table = newTable;
		threshold = cast<long>(newCapacity * loadFactor);		
	}
	
	// Node for the linked-lists in each bucket.	
	private class Node<K,V>
	{
		immutable get long hash;
		get K key;
		get set V value;
		get set nullable Node<K,V> next = null;

		public create( long initialHash, K initialKey, V initialValue )
		{
			this( initialHash, initialKey, initialValue, null);
		}
		
		public create( long initialHash, K initialKey, V initialValue, nullable Node<K,V> after )
		{
			hash = initialHash;
			key = initialKey;
			value = initialValue;			
			next = after;
		}
		
		public readonly toString() => (String)
		{
			return key # "=" # value;		
		}
	}
	
	/*
	 * Finds the hash value and index for a given key based on the provided
	 * length.
	 */
	private readonly findIndex( K key, long length ) => ( long index, long hash )
	{
		ulong temp = key->hash;
		temp ^= (temp >> 20) ^ (temp >> 12);
		long hash = cast<long>(temp ^ (temp >> 7) ^ (temp >> 4));
		return ( hash & (length - 1L), hash );	
	}
	
	/**
     * Checks whether or not the map is empty.
     * @return {@code true} if the map is empty
     */
    public readonly isEmpty() => (boolean empty)
    {
        return sizeLong == 0L;
    }	

	/**
	 * Retrieves the value associated with the key.
	 * If the key is not present, {@code null} is returned. This operation
	 * runs in constant or near-constant time.  
	 * @param key key to find
	 * @return value at key location or {@code null} if not found
	 */
	public index( K key ) => ( nullable V value )
	{		
		try
		{
			nullable var node = findKey(key);
			return check(node)->value;
		}
		recover
		{			
			return null;
		}
	}
	
	/**
	 * Stores value object in the location associated with the key. This
	 * operation runs in constant time in the best case and linear (but
	 * amortized constant) time if adding triggers a table resize.
	 * @param key key location
	 * @param value value to store
	 */ 
	public index( K key, V value ) => ()
	{
		add(key, value);
	}

	/**
	 * Stores value object in the location associated with the key and returns
	 * the old value if there was one at that location. This operation runs
	 * in constant time in the best case and linear (but amortized constant)
	 * time if adding triggers a table resize.  
	 * @param key key location
	 * @param value value to store
	 * @return old value at location or {@code null} if key was not already
	 * present
	 */ 
	public add( K key, V value ) => ( nullable V existing )
	{	
		( long index, long hash ) = findIndex(key, table->sizeLong);
		try
		{
			var current = check(table[index]);
			while ( current->hash != hash and current->key != key )
				current = check(current->next);
			V existing = current->value;
			current->value = value;
			return existing;
		}
		recover
		{
			table[index] = Node<K,V>:create(hash, key, value, table[index]);
			modifications += 1L;
			sizeLong += 1L;			
			if( sizeLong > threshold )
				resize(2L * table->sizeLong);			
			return null;
		}
	}
	
	/*
	 * Finds Node associated with a key or null if not found.	 
	 */ 
	private readonly findKey( K key ) => ( nullable Node<K,V> )
	{
		( long index, long hash ) = findIndex(key, table->sizeLong);
		try
		{
			var current = check(table[index]);
			while ( current->hash != hash and current->key != key )
				current = check(current->next);
			return current;
		}
		recover
		{			
			return null;
		}
	}
	
	/**
	 * Removes the key-value pair associated with the key location. This
	 * operation runs in constant time or near-constant time.	   
	 * @param key key to remove
	 * @return value being removed or {@code null} if not present
	 */
	public remove( K key ) => ( nullable V removed )
	{	
		( long index, long hash ) = findIndex(key, table->size);
		try
		{
			nullable Node<K,V> last = null;
			var current = check(table[index]);
			while ( current->hash != hash and current->key != key )
				( last, current ) = ( current, check(current->next) );				
			V removed = current->value;
			try
			{
				check(last)->next = current->next;
			}
			recover
			{
				table[index] = current->next;
			}
			modifications += 1L;
			return removed;
		}
		recover
		{
			return null;
		}
	}
	
	/**
	 * Checks to see if the map contains a key. This operation runs in
	 * constant time or near-constant time.	   
	 * @param key key to find
	 * @return {@code true} if present
	 */
	public readonly containsKey( K key ) => ( boolean found )
	{		
		return findKey(key) !== null;
	}
	
	/**
	 * Checks to see if the map contains a particular value.  This operation
	 * runs in time linear in the capacity of the hash table.	   
	 * @param value value to find
	 * @return {@code true} if present
	 */
	public readonly containsValue( V value ) => ( boolean found )
	{		
		foreach(V element in this)
		{
			if( value == element )
				return true;
		}
		return false;
	}

	/**
	 * Creates an iterator to iterate over all the values in the map.	   
	 * @return iterator
	 */
	public readonly iterator() => ( Iterator<V> )
	{
		return HashMapIterator<K,V>:create(this);
	}
	
	/**
	 * Removes all entries from the map and resets the capacity to default.
	 * @return map after being cleared
	 */
	public clear() => ( HashMap<K,V> )
	{
		sizeLong = 0L;
		table = Node<K,V>:null[DEFAULT_INITIAL_CAPACITY];
		threshold = cast<long>(table->sizeLong * loadFactor);
		modifications += 1L;
		return this;
	}
	
	/**
	 * Produces a {@code String} representation of the map, listing all
	 * key-value pairs in an unspecified order.
	 * @return {@code String} representation 
	 */
	public readonly toString() => (String)
	{
		var output = MutableString:create("{");
		boolean first = true;
		for( long i = 0L; i < table->sizeLong; i += 1L )
		{
			nullable var node = table[i];
			while( node !== null )
			{
				if( first )
					first = false;
				else
					output.append(", ");
				output.append(node);
				
				node = check(node)->next;							
			}
		}		
		output.append("}");
		return output.toString();
	}	
	
	/*
	 * Iterator that walks over the hash table and through the linked list at
	 * each index.
	 */
	private locked class HashMapIterator<K is CanHash and CanEqual<K>, V is CanEqual<V>> is Iterator<V>
	{
		long index = 0L;
		nullable Node<K,V> current;
		immutable long expectedModifications;
		HashMap<K,V> hashMap;
		
		public create(HashMap<K,V> hashMap)
		{
			this:hashMap = hashMap;
			expectedModifications = hashMap:modifications;
			current = next(null);
		}
		private next( nullable Node<K,V> position ) => ( nullable Node<K,V> )
		{		
			try
			{				
				position = check(position)->next;
			}
			recover{}
		
			while( position === null and index < hashMap:table->sizeLong )
			{				
				position = hashMap:table[index];				
				index += 1L;					
			}
			
			return position; //returns null if nothing left			
		}

		private readonly checkForModifications() => ()
		{
			if ( hashMap:modifications != expectedModifications )
				throw IllegalModificationException:create();
		}

		public readonly hasNext() => ( boolean )
		{
			checkForModifications();
			return current !== null;
		}

		public next() => ( V value )
		{
			checkForModifications();
			try
			{				 
				(V value, current) = ( check(current)->value, next(current) );
				return value;
			}
			recover
			{
				throw NoSuchElementException:create();
			}			
		}
	}
}
